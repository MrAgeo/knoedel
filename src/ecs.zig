//***********************************************************
// Knödel v0.1 - full zig ECS
// @Author Lorenz Mielke - https://github.com/Lommix/knoedel
// 2025
//***********************************************************

const std = @import("std");
const builtin = @import("builtin");
const assert = std.debug.assert;
const cprint = std.fmt.comptimePrint;
const json_codec = @import("json_codec.zig");

pub const AppDesc = @import("root.zig").AppDesc;

/// The memory dictator
pub const EcsAllocator = struct {
    const Self = @This();
    const Stats = struct {
        world_mem: usize,
        frame_percent: f32,
        frame_used_mb: usize,
    };
    frame_sector: []u8,
    frame_alloc: std.heap.FixedBufferAllocator,
    frame_max_alloc_perc: f32 = 0,
    parent: std.mem.Allocator,

    pub fn init(allocator: std.mem.Allocator, frame_mem: usize) !EcsAllocator {
        const frame_sector = try allocator.alloc(u8, frame_mem);
        return .{
            .frame_sector = frame_sector,
            .frame_alloc = std.heap.FixedBufferAllocator.init(frame_sector),
            .parent = allocator,
        };
    }

    pub fn refreshVTable(self: *Self, gpa: std.mem.Allocator) void {
        self.parent = gpa;
    }

    pub fn deinit(self: *Self) void {
        self.parent.free(self.frame_sector);
        self.frame_alloc.reset();
    }

    pub fn world(self: *Self) std.mem.Allocator {
        return self.parent;
    }

    pub fn frame(self: *Self) std.mem.Allocator {
        return self.frame_alloc.threadSafeAllocator();
    }

    pub fn resetFrame(self: *Self) void {
        const frame_percent: f32 = @floatCast(@as(f64, @floatFromInt(self.frame_alloc.end_index)) / @as(f64, @floatFromInt(self.frame_sector.len)));
        self.frame_max_alloc_perc = frame_percent;
        self.frame_alloc.reset();
    }

    pub fn stats(self: *Self) Stats {
        return Stats{
            .world_mem = 0,
            .frame_percent = @floatCast(@as(f64, @floatFromInt(self.frame_alloc.end_index)) / @as(f64, @floatFromInt(self.frame_sector.len))),
            .frame_used_mb = @divTrunc(self.frame_alloc.end_index, @import("root.zig").MB),
        };
    }
};

// ------------------------------------------------------------------------------
/// Error Union of what can go wrong
/// # TODO: needs some cleanup and renaming
pub const EcsError = error{
    ResourceNotFound,
    SystemFailure,
    SystemConditionFailure,
    DuplicateSystemRegistration,
    ComponentNotFound,
    EntityNotFound,
    UnknownType,
    SerializeError,
    DeserlizeError,
    EndOfStream,
    ComponentListMismatch,
    TypeIsNotInQuery,
} || std.mem.Allocator.Error || anyerror;

pub const Entity = enum(u64) {
    placeholder,
    _,

    pub inline fn id(ent: *const Entity) u32 {
        return ent.fields().idx;
    }

    pub inline fn gen(ent: *const Entity) u32 {
        return ent.fields().gen;
    }

    pub inline fn new(idx: u32) Entity {
        const f = Fields{ .idx = idx, .gen = 0 };
        return @as(*const Entity, @ptrCast(&f)).*;
    }

    pub fn incGen(ent: *Entity) void {
        var f = ent.fields();
        f.gen += 1;

        ent.* = @as(*const Entity, @ptrCast(&f)).*;
    }

    inline fn fields(ent: *const Entity) Fields {
        return @as(*const Fields, @ptrCast(ent)).*;
    }

    const Fields = packed struct {
        idx: u32,
        gen: u32,
    };
};

pub const Parent = struct {
    entity: Entity = .placeholder,
};

pub const Children = struct {
    items: std.ArrayList(Entity) = .empty,

    pub fn slice(self: *const Children) []const Entity {
        return self.items.items;
    }

    pub fn deinit(self: *Children, gpa: std.mem.Allocator) void {
        self.items.deinit(gpa);
    }
};

// ------------------------------------------------------------------------------

/// The core ECS struct. Owner of everything
/// Needs to have a fixed position in memory and called init on.
pub fn App(comptime desc: AppDesc) type {
    return struct {
        // ----------------------------------------
        memtator: EcsAllocator,
        entities: struct {
            entity_mutex: std.Io.Mutex = .init,
            unused: std.ArrayList(Entity) = .empty,
            count: u32 = 0,
        } = .{},

        components: ComponentRegistry(desc.FlagInt) = .{},
        resources: ResourceRegistry(desc.FlagInt) = .{},

        systems: SystemRegistry = .{},
        commands: CommandRegistry = .{},
        hooks: HookRegistry(desc) = .{},
        world_tick: u32 = 0,
        io: std.Io,
        // ---------------------------------------
        const World = @This();

        pub fn init(gpa: std.mem.Allocator, io: std.Io) EcsError!*World {
            const memtator = try EcsAllocator.init(gpa, desc.max_frame_mem);
            const systems = try SystemRegistry.init(gpa);
            const self = try gpa.create(World);

            self.* = .{
                .memtator = memtator,
                .systems = systems,
                .io = io,
            };

            return self;
        }

        ///! valid check
        pub fn isValid(self: *const World, ent: Entity) bool {
            return self.components.entity_lookup.contains(ent);
        }

        pub fn deinit(self: *World) void {
            const gpa = self.memtator.world();
            self.entities.unused.deinit(gpa);
            self.commands.queue.deinit(gpa);
            self.components.releaseAllComponentRegistryMemory(gpa);
            self.resources.deinit(gpa);
            self.systems.releaseAllSystemRegistryMemory(gpa);
            self.hooks.releaseAllHookRegistryMemory(gpa);
            self.memtator.deinit();
            gpa.destroy(self);
        }

        /// modify the world and a thread safe way
        pub fn getCommands(self: *World) Commands {
            return Commands{
                .world = self,
                .reg = &self.commands,
                .frame_gpa = self.memtator.frame(),
                .world_gpa = self.memtator.world(),
            };
        }

        /// run a system schedule in lock free parallel
        pub fn runPar(self: *World, schedule: anytype) void {
            self.systems.runPar(schedule, self) catch |err| {
                std.log.err("system failed with `{any}`", .{err});
            };
        }

        /// run a system schedule in 'order' (depending on order added)
        pub fn run(self: *World, schedule: anytype) void {
            self.systems.run(schedule, self) catch |err| {
                std.log.err("system failed with `{any}`", .{err});
            };
        }

        /// run any system right now, pass any *const fn ptr.
        /// does not allow for `local access`
        pub fn runInstant(self: *World, system_fn: anytype) !void {
            const FnType = @typeInfo(@TypeOf(system_fn)).pointer.child;
            const info = @typeInfo(FnType);

            var sys_args: SystemRegistry.genArgType(info.@"fn".param_types) = undefined;

            inline for (info.@"fn".param_types, 0..) |param_type, i| {
                const Ty = param_type orelse @compileError("generic parameter not allowed");
                if (@hasDecl(Ty, "fromWorld")) {
                    var ret = try Ty.fromWorld(self);
                    if (@hasDecl(Ty, "setWorldTick")) ret.setWorldTick(self.world_tick);
                    @field(sys_args, cprint("{d}", .{i})) = ret;
                    continue;
                }
            }

            try @call(.auto, system_fn, sys_args);
        }

        /// insert a resource
        pub fn addResource(self: *World, res: anytype) !void {
            try self.resources.register(self.memtator.world(), res);
        }

        /// does not overwrite existing resource
        pub fn tryAddResource(self: *World, res: anytype) !void {
            try self.resources.tryRegister(self.memtator.world(), res);
        }

        pub fn addOnDespawnHook(
            self: *World,
            comptime T: type,
            comptime hook_fn: *const fn (*T, Entity, *World) EcsError!void,
        ) EcsError!void {
            try self.hooks.OnDespawnComp(self, self.memtator.world(), T, hook_fn);
        }

        pub fn addOnAddHook(
            self: *World,
            comptime T: type,
            comptime hook_fn: *const fn (*T, Entity, *World) EcsError!void,
        ) EcsError!void {
            try self.hooks.OnAddComp(self, self.memtator.world(), T, hook_fn);
        }

        pub fn addOnRemoveHook(
            self: *World,
            comptime T: type,
            comptime hook_fn: *const fn (*T, Entity, *World) EcsError!void,
        ) EcsError!void {
            try self.hooks.OnRemoveComp(self, self.memtator.world(), T, hook_fn);
        }

        /// add a system
        /// accepts single function or tuple of function.
        pub fn addSystem(self: *World, schedule: anytype, comptime func: anytype) !void {
            try self.systems.add(self.memtator.world(), self, schedule, func, null);
        }

        /// add a system with a run time condition
        /// accepts single function or tuple of functions.
        pub fn addSystemEx(self: *World, schedule: anytype, comptime run_fn: anytype, comptime condition_fn: SystemRegistry.ConditionFn) !void {
            try self.systems.add(self.memtator.world(), self, schedule, run_fn, condition_fn);
        }

        ///! get a resource
        pub fn resource(self: *const World, comptime R: type) EcsError!*R {
            return self.resources.get(R) orelse return EcsError.ResourceNotFound;
        }

        /// get a resource by type
        pub fn getResource(self: *const World, comptime R: type) EcsError!*R {
            return self.resources.get(R) orelse return EcsError.ResourceNotFound;
        }

        /// get a resource by type
        /// creates a default impl of the type and returns it. Type must have defaults!
        pub fn getOrDefaultResource(self: *World, comptime R: type) EcsError!*R {
            return try self.resources.getOrDefault(self.memtator.world(), R);
        }

        /// update tick
        /// runs all remainig commands and resets the frame arena
        pub fn update(self: *World) void {
            self.flushCommands();
            _ = self.memtator.resetFrame();
            self.world_tick = self.world_tick +% 1;
        }

        pub const ComponentHeader = struct {
            hash: u32,
            name: []const u8,
            size: usize,
        };

        pub const ResourceHeader = struct {
            hash: u32,
            name: []const u8,
            size: usize,
        };

        pub const SceneWriteEvent = union(enum) {
            begin_scene,
            end_scene,
            begin_entity: Entity,
            end_entity: Entity,
            begin_component: ComponentHeader,
            end_component: ComponentHeader,
            begin_resource: ResourceHeader,
            end_resource: ResourceHeader,
        };

        pub const SceneReadEvent = union(enum) {
            begin_scene,
            end_scene,
            begin_entity: Entity,
            end_entity,
            begin_component: ComponentHeader,
            end_component,
            begin_resource: ResourceHeader,
            end_resource,
        };

        pub const SceneCodec = struct {
            ptr: *anyopaque,
            /// Owns all scene-level framing, names, delimiters, counts, and bytecode.
            writeEvent: WriteEventFn,
            readEvent: ReadEventFn,
            /// Skips only the current component payload. ECS still consumes `end_component`.
            skipComponentPayload: SkipEventFn,
            /// Skips only the current resource payload. ECS still consumes `end_resource`.
            skipResourcePayload: SkipResourceFn,

            pub const WriteEventFn = *const fn (*anyopaque, SceneWriteEvent, *std.Io.Writer) anyerror!void;
            pub const ReadEventFn = *const fn (*anyopaque, *std.Io.Reader) anyerror!SceneReadEvent;
            pub const SkipEventFn = *const fn (*anyopaque, ComponentHeader, *std.Io.Reader) anyerror!void;
            pub const SkipResourceFn = *const fn (*anyopaque, ResourceHeader, *std.Io.Reader) anyerror!void;
        };

        pub fn registerComponentCodec(
            self: *World,
            comptime C: type,
            name: []const u8,
            comptime serializeFn: *const fn (*const C, *std.Io.Writer) anyerror!void,
            comptime deserializeFn: *const fn (*C, std.mem.Allocator, *std.Io.Reader) anyerror!void,
        ) !void {
            try self.components.registerCodec(self.memtator.world(), C, .{
                .name = name,
                .serialize = (struct {
                    fn run(ptr: *const anyopaque, w: *std.Io.Writer) anyerror!void {
                        const comp: *const C = @ptrCast(@alignCast(ptr));
                        try serializeFn(comp, w);
                    }
                }).run,
                .deserialize = (struct {
                    fn run(ptr: *anyopaque, allocator: std.mem.Allocator, r: *std.Io.Reader) anyerror!void {
                        const comp: *C = @ptrCast(@alignCast(ptr));
                        try deserializeFn(comp, allocator, r);
                    }
                }).run,
            });
        }

        pub fn registerResourceCodec(
            self: *World,
            comptime R: type,
            name: []const u8,
            comptime serializeFn: *const fn (*const R, *std.Io.Writer) anyerror!void,
            comptime deserializeFn: *const fn (*R, std.mem.Allocator, *std.Io.Reader) anyerror!void,
        ) !void {
            try self.resources.registerCodec(self.memtator.world(), R, .{
                .name = name,
                .serialize = (struct {
                    fn run(ptr: *const anyopaque, w: *std.Io.Writer) anyerror!void {
                        const res: *const R = @ptrCast(@alignCast(ptr));
                        try serializeFn(res, w);
                    }
                }).run,
                .deserializeRegister = (struct {
                    fn run(reg: *ResourceRegistry(desc.FlagInt), gpa: std.mem.Allocator, r: *std.Io.Reader) anyerror!void {
                        var res: R = undefined;
                        try deserializeFn(&res, gpa, r);
                        try reg.register(gpa, res);
                    }
                }).run,
            });
        }

        pub fn importScene(self: *World, r: *std.Io.Reader, scene: SceneCodec) !void {
            switch (try scene.readEvent(scene.ptr, r)) {
                .begin_scene => {},
                else => return error.MalformedScene,
            }

            var reading_resources = false;
            while (true) {
                switch (try scene.readEvent(scene.ptr, r)) {
                    .end_scene => return,
                    .begin_entity => |entity| {
                        if (reading_resources) return error.MalformedScene;
                        try self.importEntityAfterBegin(entity, r, scene);
                    },
                    .begin_resource => |header| {
                        reading_resources = true;
                        try self.importResourceAfterBegin(header, r, scene);
                    },
                    else => return error.MalformedScene,
                }
            }
        }

        pub fn importEntity(self: *World, entity: Entity, r: *std.Io.Reader, scene: SceneCodec) !void {
            switch (try scene.readEvent(scene.ptr, r)) {
                .begin_entity => try self.importEntityAfterBegin(entity, r, scene),
                else => return error.MalformedScene,
            }
        }

        fn importEntityAfterBegin(self: *World, entity: Entity, r: *std.Io.Reader, scene: SceneCodec) !void {
            try self.claimEntityId(entity);

            while (true) {
                switch (try scene.readEvent(scene.ptr, r)) {
                    .end_entity => return,
                    .begin_component => |header| {
                        const codec = self.components.codecs.get(header.hash) orelse {
                            try scene.skipComponentPayload(scene.ptr, header, r);
                            try self.readEndComponent(r, scene);
                            continue;
                        };
                        const flag = self.components.component_flags.getFlagFromHash(header.hash) orelse {
                            try scene.skipComponentPayload(scene.ptr, header, r);
                            try self.readEndComponent(r, scene);
                            continue;
                        };
                        const info = self.components.component_flags.getId(flag);
                        if (header.size != 0 and header.size != info.size) return error.ComponentListMismatch;

                        const raw = try self.allocComponentTemp(info.size, info.alignment);
                        defer self.freeComponentTemp(raw, info.alignment);

                        try codec.deserialize(raw.ptr, self.memtator.world(), r);
                        try self.components.addRaw(self.memtator.world(), self.world_tick, entity, flag, raw);

                        try self.readEndComponent(r, scene);
                    },
                    else => return error.MalformedScene,
                }
            }
        }

        fn importResourceAfterBegin(self: *World, header: ResourceHeader, r: *std.Io.Reader, scene: SceneCodec) !void {
            const codec = self.resources.codecs.get(header.hash) orelse {
                try scene.skipResourcePayload(scene.ptr, header, r);
                try self.readEndResource(r, scene);
                return;
            };
            const flag = self.resources.resource_flags.getFlagFromHash(header.hash) orelse {
                try scene.skipResourcePayload(scene.ptr, header, r);
                try self.readEndResource(r, scene);
                return;
            };
            const info = self.resources.resource_flags.getId(flag);
            if (header.size != 0 and header.size != info.size) return error.ResourceListMismatch;

            try codec.deserializeRegister(&self.resources, self.memtator.world(), r);
            try self.readEndResource(r, scene);
        }

        fn readEndComponent(_: *World, r: *std.Io.Reader, scene: SceneCodec) !void {
            switch (try scene.readEvent(scene.ptr, r)) {
                .end_component => {},
                else => return error.MalformedScene,
            }
        }

        fn readEndResource(_: *World, r: *std.Io.Reader, scene: SceneCodec) !void {
            switch (try scene.readEvent(scene.ptr, r)) {
                .end_resource => {},
                else => return error.MalformedScene,
            }
        }

        fn allocComponentTemp(self: *World, size: usize, alignment: usize) ![]u8 {
            if (size == 0) return &.{};
            const ptr = self.memtator.world().rawAlloc(size, .fromByteUnits(alignment), @returnAddress()) orelse return error.OutOfMemory;
            return ptr[0..size];
        }

        fn freeComponentTemp(self: *World, bytes: []u8, alignment: usize) void {
            if (bytes.len == 0) return;
            self.memtator.world().rawFree(bytes, .fromByteUnits(alignment), @returnAddress());
        }

        pub fn exportScene(self: *World, w: *std.Io.Writer, scene: SceneCodec, comptime Ctag: type) !void {
            try scene.writeEvent(scene.ptr, .begin_scene, w);

            const q = try QueryF(struct { e: Entity }, .With(Ctag)).fromWorld(self);
            var it = q.iter();

            while (it.next()) |en| {
                self.exportEntity(en.e, w, scene) catch |err| switch (err) {
                    error.EntityHasNoExportComponents => {},
                    else => |e| return e,
                };
            }

            try self.exportResources(w, scene);
            try scene.writeEvent(scene.ptr, .end_scene, w);
        }

        pub fn exportEntity(self: *const World, entity: Entity, w: *std.Io.Writer, scene: SceneCodec) !void {
            const arch_id = self.components.entity_lookup.get(entity) orelse return error.EntityNotFound;
            const arch = &self.components.archtypes.items[arch_id];
            const entity_arch_index = arch.entity_lookup.get(entity).?;

            var has_export_comps = false;
            for (arch.columns.items) |*col| {
                _ = self.components.codecs.get(col.hash) orelse continue;
                has_export_comps = true;
            }

            if (!has_export_comps) return error.EntityHasNoExportComponents;

            try scene.writeEvent(scene.ptr, .{ .begin_entity = entity }, w);

            for (arch.columns.items) |*col| {
                const codec = self.components.codecs.get(col.hash) orelse continue;
                const raw = arch.getSingleRaw(entity_arch_index, col);
                const header = ComponentHeader{
                    .hash = col.hash,
                    .name = codec.name,
                    .size = col.size,
                };

                try scene.writeEvent(scene.ptr, .{ .begin_component = header }, w);
                try codec.serialize(raw.ptr, w);
                try scene.writeEvent(scene.ptr, .{ .end_component = header }, w);
            }

            try scene.writeEvent(scene.ptr, .{ .end_entity = entity }, w);
        }

        pub fn exportResources(self: *const World, w: *std.Io.Writer, scene: SceneCodec) !void {
            var it = self.resources.data.iterator();
            while (it.next()) |entry| {
                const hash = entry.key_ptr.*;
                const codec = self.resources.codecs.get(hash) orelse continue;
                const flag = self.resources.resource_flags.getFlagFromHash(hash) orelse continue;
                const info = self.resources.resource_flags.getId(flag);
                const header = ResourceHeader{
                    .hash = hash,
                    .name = codec.name,
                    .size = info.size,
                };

                try scene.writeEvent(scene.ptr, .{ .begin_resource = header }, w);
                try codec.serialize(entry.value_ptr.ctx, w);
                try scene.writeEvent(scene.ptr, .{ .end_resource = header }, w);
            }
        }

        /// flush the command queue
        /// not thread safe. Should be called between scheduels
        pub fn flushCommands(self: *World) void {
            self.commands.runAllUnsafe(self);
        }

        fn despawn_with_children(self: *World, ent: Entity) EcsError!void {
            const arch_id = self.components.entity_lookup.get(ent) orelse return;

            // ----------------------------------------
            // hook
            const mask = self.components.archtypes.items[arch_id].mask;
            const hook_mask = mask.intersectWith(self.hooks.has_despawn_hook);
            var it = hook_mask.iterator();
            while (it.next()) |flag| {
                const ptr = self.components.getSingleOpaque(ent, flag).?;
                try self.hooks.runDespawnHook(flag, ptr, ent, self);
            }
            // ----------------------------------------

            if (self.components.getSingle(ent, Children)) |children| {
                for (children.items.items) |child| {
                    try self.despawn_with_children(child);
                }
                children.deinit(self.memtator.world());
                children.items = .empty;
            }

            try self.components.despawn(self.memtator.world(), ent);
        }

        fn despawn(self: *World, ent: Entity, include_children: bool) EcsError!void {
            if (!self.isValid(ent)) return;
            // remove from parent children
            if (self.components.getSingle(ent, Parent)) |parent| {
                if (self.components.getSingle(parent.entity, Children)) |children| {
                    var index: ?usize = null;
                    for (children.items.items, 0..) |child, i| {
                        if (child == ent) index = i;
                    }
                    if (index) |i| _ = children.items.swapRemove(i);
                }
            }

            if (include_children) {
                try self.despawn_with_children(ent);
            } else {
                if (self.components.getSingle(ent, Children)) |children| {
                    for (children.items.items) |child_entity| {
                        try self.components.remove(self.memtator.world(), child_entity, Parent);
                    }
                }

                try self.components.despawn(self.memtator.world(), ent);
            }
            try self.entities.unused.append(self.memtator.world(), ent);
        }

        /// total used entities
        pub fn entityCount(self: *World) usize {
            return @intCast(self.components.entity_lookup.size);
        }

        /// add a plugin. Any struct that implements a `plugin(app:*App) !void` is considered a plugin.
        pub fn addPlugin(self: *World, mod: type) !void {
            try mod.plugin(self);
        }

        /// threadsafe next entity id
        pub fn nextEntityId(self: *World) Entity {
            self.entities.entity_mutex.lockUncancelable(self.io);
            defer self.entities.entity_mutex.unlock(self.io);

            if (self.entities.unused.items.len > 0) {
                var ent = self.entities.unused.pop().?;
                ent.incGen();
                return ent;
            } else {
                const ent = Entity.new(self.entities.count + 1); // avoid using 0 which is our .placeholder
                self.entities.count += 1;
                return ent;
            }
        }

        /// Claim a specific entity id for use. Despawns any existing entity
        /// occupying the same slot first.
        pub fn claimEntityId(self: *World, entity: Entity) EcsError!void {
            if (self.liveEntityWithId(entity.id())) |live_entity| {
                try self.despawn(live_entity, true);
            }

            self.entities.entity_mutex.lockUncancelable(self.io);
            defer self.entities.entity_mutex.unlock(self.io);

            const idx = entity.id();

            // remove from unused pool
            var i: usize = 0;
            while (i < self.entities.unused.items.len) {
                if (self.entities.unused.items[i].id() == idx) {
                    _ = self.entities.unused.swapRemove(i);
                } else {
                    i += 1;
                }
            }

            // extend count if needed, filling gaps into unused
            if (idx > self.entities.count) {
                var gap: u32 = self.entities.count + 1;
                while (gap < idx) : (gap += 1) {
                    try self.entities.unused.append(self.memtator.world(), Entity.new(gap));
                }
                self.entities.count = idx;
            }
        }

        fn liveEntityWithId(self: *World, idx: u32) ?Entity {
            var it = self.components.entity_lookup.iterator();
            while (it.next()) |entry| {
                if (entry.key_ptr.id() == idx) return entry.key_ptr.*;
            }
            return null;
        }

        /// main system scheduler
        pub const SystemRegistry = struct {
            // --------------------------
            systems: std.AutoHashMapUnmanaged(SystemID, OpaqueSystem) = .empty,
            locals: std.AutoHashMapUnmanaged(SystemID, LocalRegistry(desc.FlagInt)) = .empty,
            schedule_order: std.AutoHashMapUnmanaged(ScheduleID, Schedule) = .empty,
            // --------------------------

            const Self = @This();
            const LocalRegistry = ResourceRegistry;
            pub const ConditionFn = *const fn (*World, *LocalRegistry(desc.FlagInt)) EcsError!bool;
            pub const SystemFn = *const fn (*anyopaque, *World, *LocalRegistry(desc.FlagInt), u32) EcsError!void;
            pub const SystemID = u32;
            const ScheduleID = u32;

            /// a system's mem represntation
            pub const OpaqueSystem = struct {
                access: Access(desc.FlagInt),
                ptr: *anyopaque,
                run: SystemRegistry.SystemFn,
                condition: ?SystemRegistry.ConditionFn = null,
                debug: []u8,
                run_time_ns: i128 = 0,
                batch_id: usize = 0,
                last_run_tick: u32 = 0,
            };

            pub const Schedule = struct {
                systems: std.ArrayList(struct {
                    id: SystemID,
                    deps: ?std.ArrayList(SystemID) = null,
                }) = .empty,
                batch_count: usize = 0,
                run_time_ns: i128 = 0,
            };

            pub fn init(_: std.mem.Allocator) !Self {
                return .{};
            }

            /// hot-reload teardown: frees systems and schedule order, keeps `locals`.
            /// Systems re-register under a stable name hash and must reattach
            /// their locals (`OnEnter` observers, `kn.Local` state).
            pub fn clear(self: *Self, gpa: std.mem.Allocator) void {
                var systemDebugIterator = self.systems.iterator();
                while (systemDebugIterator.next()) |systemEntry| {
                    gpa.free(systemEntry.value_ptr.debug);
                }
                self.systems.deinit(gpa);
                self.systems = .empty;
                var scheduleIterator = self.schedule_order.iterator();
                while (scheduleIterator.next()) |scheduleEntry| {
                    for (scheduleEntry.value_ptr.systems.items) |*scheduledSystemEntry| {
                        if (scheduledSystemEntry.deps) |*dependencyList| {
                            dependencyList.deinit(gpa);
                        }
                    }
                    scheduleEntry.value_ptr.systems.deinit(gpa);
                }
                self.schedule_order.deinit(gpa);
                self.schedule_order = .empty;
            }

            /// full teardown: `clear` plus per-system locals
            pub fn releaseAllSystemRegistryMemory(self: *Self, gpa: std.mem.Allocator) void {
                self.clear(gpa);
                var localRegistryIterator = self.locals.iterator();
                while (localRegistryIterator.next()) |localRegistryEntry| {
                    localRegistryEntry.value_ptr.deinit(gpa);
                }
                self.locals.deinit(gpa);
                self.locals = .empty;
            }

            pub fn getScheduleTime(self: *SystemRegistry, schedule: anytype) i128 {
                comptime {
                    if (@typeInfo(@TypeOf(schedule)) != .@"enum") @compileError("schedule needs to be of type enum");
                }
                const set = self.schedule_order.getPtr(@intFromEnum(schedule)) orelse return 0;
                return set.run_time_ns;
            }

            /// Extracting the original function path from the return type.
            inline fn extractFnName(comptime func: anytype) []const u8 {
                if (!@inComptime()) @compileError("lol");
                const fn_type = @typeInfo(@TypeOf(func)).pointer.child;
                const info = @typeInfo(fn_type);
                const ret_str: []const u8 = @typeName(info.@"fn".return_type.?);

                return comptime blk: {
                    if (ret_str.len <= 28) {
                        // TODO: this is a bug, same name, same hash, same locals
                        break :blk "anonym";
                    }

                    const fstr = ret_str[28..]; // constrained by system signature, won't move
                    var c: u32 = 0;
                    while (fstr[c] != ')') {
                        c += 1;
                    }
                    break :blk fstr[0..c];
                };
            }

            const ScheduleStats = struct {
                pub const InfoEntry = struct {
                    batch_id: usize,
                    name: []u8,
                    avg_ns: i128,
                };
                avg_ns: i128 = 0,
                batch_count: usize = 0,
                batches: std.ArrayList(InfoEntry) = .empty,
            };

            pub fn scheduleInfo(self: *const Self, gpa: std.mem.Allocator, schedule: anytype) !ScheduleStats {
                const set: *Schedule = self.schedule_order.getPtr(@intFromEnum(schedule)) orelse return error.NotFound;
                var info = ScheduleStats{};

                for (set.systems.items) |*en| {
                    const sys = self.systems.getPtr(en.id) orelse continue;
                    try info.batches.append(gpa, .{
                        .batch_id = sys.batch_id,
                        .name = sys.debug,
                        .avg_ns = sys.run_time_ns,
                    });
                }

                info.batch_count = set.batch_count;
                info.avg_ns = set.run_time_ns;
                return info;
            }

            fn putSystem(
                self: *Self,
                gpa: std.mem.Allocator,
                app: *App(desc),
                comptime system: anytype,
                comptime condition_fn: ?ConditionFn,
            ) EcsError!SystemID {
                const func = system;
                const fn_name = comptime extractFnName(func);
                const hash = comptime hashStr(@typeName(@TypeOf(system)));

                if (self.systems.contains(hash)) {
                    std.log.err("duplicate system `{s}`", .{fn_name});
                    return EcsError.DuplicateSystemRegistration;
                }

                if (@typeInfo(@TypeOf(func)) != .pointer) @compileError(cprint("system needs to be pointer in `{s}`", .{fn_name}));
                if (@typeInfo(@typeInfo(@TypeOf(func)).pointer.child) != .@"fn") @compileError(cprint("system needs to be pointer `{s}`", .{fn_name}));

                const fnType = @typeInfo(@TypeOf(func)).pointer.child;
                const info = @typeInfo(fnType);

                var access: Access(desc.FlagInt) = .{};
                inline for (info.@"fn".param_types) |maybe_pt| {
                    const PT = maybe_pt orelse @compileError("generic function not allowed here");
                    if (@typeInfo(PT) != .@"struct") @compileError(cprint("System param must be struct with method `fromWorld(w:*World)Self` in `{s}::{s}`\n", .{ fn_name, @typeName(PT) }));

                    if (@hasDecl(PT, "addAccess")) {
                        PT.addAccess(app, &access);
                    } else {
                        switch (@typeInfo(PT)) {
                            .@"struct" => |str| {
                                inline for (str.field_types) |f_type| {
                                    if (@typeInfo(f_type) != .@"struct") continue;
                                    if (@hasDecl(f_type, "addAccess")) {
                                        f_type.addAccess(app, &access);
                                    }
                                }
                            },
                            else => {},
                        }
                    }
                }

                const op_system = OpaqueSystem{
                    .ptr = @constCast(func),
                    .access = access,
                    .condition = condition_fn,
                    .debug = try gpa.dupe(u8, fn_name),
                    .run = (struct {
                        fn run(ptr: *anyopaque, world: *World, locals: *LocalRegistry(desc.FlagInt), last_run_tick: u32) EcsError!void {
                            @setEvalBranchQuota(6400);
                            const sys_func: *fnType = @ptrCast(@alignCast(ptr));
                            var sys_args: genArgType(info.@"fn".param_types) = undefined;
                            inline for (info.@"fn".param_types, 0..) |param_type, i| {
                                const PT: type = switch (@typeInfo(param_type orelse @compileError("generic parameter not allowed"))) {
                                    .@"struct" => param_type.?,
                                    else => @compileError("not a valid system param type, needs to be a struct"),
                                };

                                if (@hasDecl(PT, "fromLocal")) {
                                    var ret = try PT.fromLocal(world, locals);
                                    if (@hasDecl(PT, "setWorldTick")) ret.setWorldTick(last_run_tick);
                                    @field(sys_args, cprint("{d}", .{i})) = ret;
                                    continue;
                                }

                                if (@hasDecl(PT, "fromWorld")) {
                                    var ret = try PT.fromWorld(world);
                                    if (@hasDecl(PT, "setWorldTick")) ret.setWorldTick(last_run_tick);
                                    @field(sys_args, cprint("{d}", .{i})) = ret;
                                    continue;
                                }

                                if (@hasDecl(PT, "is_local_marker")) {
                                    const res = try locals.getOrDefault(world.memtator.world(), PT.innerType);
                                    @field(sys_args, cprint("{d}", .{i})) = PT{ .inner = res };
                                    continue;
                                }

                                switch (@typeInfo(PT)) {
                                    .@"struct" => |str| {
                                        var compound: PT = undefined;
                                        inline for (str.field_names, str.field_types) |f_name, f_type| {

                                            if (@hasDecl(f_type, "fromLocal")) {
                                                var ret = try f_type.fromLocal(world, locals);
                                                if (@hasDecl(f_type, "setWorldTick")) ret.setWorldTick(last_run_tick);
                                                @field(compound, f_name) = ret;
                                                continue;
                                            }

                                            if (@hasDecl(f_type, "fromWorld")) {
                                                var ret = try f_type.fromWorld(world);
                                                if (@hasDecl(f_type, "setWorldTick")) ret.setWorldTick(last_run_tick);
                                                @field(compound, f_name) = ret;
                                                continue;
                                            }

                                            if (@hasDecl(f_type, "is_local_marker")) {
                                                const res = try locals.getOrDefault(world.memtator.world(), f_type.innerType);
                                                @field(compound, f_name) = f_type{ .inner = res };
                                                continue;
                                            }

                                            @compileError(cprint("compound system param does not implement fromWorld! (fn(world:*App)Self)  `{s}::{s}`\n", .{ fn_name, @typeName(PT) }));
                                        }

                                        @field(sys_args, cprint("{d}", .{i})) = compound;
                                        continue;
                                    },
                                    else => {},
                                }

                                @compileError(cprint("system param does not implement fromWorld! (fn(world:*App)Self)  `{s}::{s}`\n", .{ fn_name, @typeName(PT) }));
                            }

                            try @call(.auto, sys_func, sys_args);
                        }
                    }).run,
                };
                try self.systems.put(gpa, hash, op_system);

                if (!self.locals.contains(hash)) {
                    try self.locals.putNoClobber(gpa, hash, .{});
                }

                return hash;
            }

            pub fn add(
                self: *Self,
                gpa: std.mem.Allocator,
                app: *App(desc),
                schedule: anytype,
                comptime system: anytype,
                comptime condition_fn: ?ConditionFn,
            ) EcsError!void {
                const set = try self.schedule_order.getOrPut(gpa, @intFromEnum(schedule));
                if (!set.found_existing) set.value_ptr.* = .{};

                const SystemType = @TypeOf(system);
                const info = @typeInfo(SystemType);

                switch (info) {
                    .@"struct" => |_struct| {
                        if (!_struct.is_tuple) @compileLog("system must be tuple");
                        inline for (system) |func| {
                            const id = try self.putSystem(gpa, app, func, condition_fn);
                            try set.value_ptr.systems.append(gpa, .{ .id = id });
                        }
                    },
                    .type => {
                        if (@hasDecl(system, "_is_chain")) {
                            var accumulatedDependencyIds: std.ArrayList(SystemID) = .empty;
                            defer accumulatedDependencyIds.deinit(gpa);
                            inline for (system.inner) |field| {
                                const field_ty = @TypeOf(field);
                                switch (@typeInfo(field_ty)) {
                                    .@"struct" => |_struct| {
                                        if (!_struct.is_tuple) @compileLog("only pointers and tuples allowed in chains");
                                        var snapshotDependencyIds = try accumulatedDependencyIds.clone(gpa);
                                        defer snapshotDependencyIds.deinit(gpa);
                                        inline for (field) |func| {
                                            const id = try self.putSystem(gpa, app, func, condition_fn);
                                            try set.value_ptr.systems.append(gpa, .{
                                                .id = id,
                                                .deps = if (snapshotDependencyIds.items.len > 0) try snapshotDependencyIds.clone(gpa) else null,
                                            });

                                            try accumulatedDependencyIds.append(gpa, id);
                                        }
                                    },
                                    else => {
                                        const id = try self.putSystem(gpa, app, field, condition_fn);
                                        try set.value_ptr.systems.append(gpa, .{
                                            .id = id,
                                            .deps = if (accumulatedDependencyIds.items.len > 0) try accumulatedDependencyIds.clone(gpa) else null,
                                        });

                                        try accumulatedDependencyIds.append(gpa, id);
                                    },
                                }
                            }
                        }
                    },
                    else => {
                        const id = try self.putSystem(gpa, app, system, condition_fn);
                        try set.value_ptr.systems.append(gpa, .{ .id = id });
                    },
                }
            }

            fn genArgType(comptime maybe_types: []const ?type) type {
                var types: [maybe_types.len]type = undefined;
                for (&types, maybe_types) |*d, s| d.* = s orelse @compileError("generic parameter not allowed");
                return @Tuple(&types);
            }

            pub fn run(self: *Self, schedule: anytype, world: *World) !void {
                if (@typeInfo(@TypeOf(schedule)) != .@"enum") @compileError("schedule needs to be of type enum");

                const set = self.schedule_order.getPtr(@intFromEnum(schedule)) orelse return;

                const gpa = world.memtator.frame();
                var scheduled_systems = try std.ArrayList(SystemID).initCapacity(gpa, 32);
                var active_systems = std.AutoHashMap(SystemID, void).init(gpa);
                var dep_graph = std.AutoHashMap(SystemID, std.ArrayList(SystemID)).init(gpa);
                var dep_counter = std.AutoHashMap(SystemID, usize).init(gpa);

                const start = std.Io.Clock.Timestamp.now(world.io, .awake);

                for (set.systems.items) |entry| {
                    const sys = self.systems.getPtr(entry.id).?;
                    const locals = self.locals.getPtr(entry.id).?;

                    if (sys.condition) |con| {
                        const should_run = con(world, locals) catch {
                            return EcsError.SystemConditionFailure;
                        };

                        if (!should_run) continue;
                    }
                    try scheduled_systems.append(gpa, entry.id);
                    try active_systems.put(entry.id, {});
                }

                for (set.systems.items) |entry| {
                    if (!active_systems.contains(entry.id)) continue;
                    if (entry.deps) |deps| {
                        var active_dep_count: usize = 0;
                        for (deps.items) |dep_id| {
                            if (!active_systems.contains(dep_id)) continue;
                            active_dep_count += 1;
                            const res = try dep_graph.getOrPut(dep_id);
                            if (!res.found_existing) res.value_ptr.* = .empty;
                            try res.value_ptr.append(gpa, entry.id);
                        }
                        if (active_dep_count > 0) try dep_counter.put(entry.id, active_dep_count);
                    }
                }

                while (scheduled_systems.items.len > 0) {
                    var remaining: usize = 0;
                    for (scheduled_systems.items) |id| {
                        if (dep_counter.get(id)) |count| if (count > 0) {
                            scheduled_systems.items[remaining] = id;
                            remaining += 1;
                            continue;
                        };

                        const sys = self.systems.getPtr(id).?;
                        const locals = self.locals.getPtr(id).?;

                        executeSystem(sys, locals, world, 0);

                        if (dep_graph.getPtr(id)) |deps| {
                            for (deps.items) |dep_id| {
                                //reduce dep counter
                                if (dep_counter.getPtr(dep_id)) |count| count.* = count.* - 1;
                            }
                        }
                    }

                    if (remaining == scheduled_systems.items.len) return EcsError.SystemFailure;
                    scheduled_systems.items.len = remaining;
                }

                const total: i128 = start.untilNow(world.io).raw.toNanoseconds();
                set.run_time_ns = @divTrunc(set.run_time_ns + total, 2);
                set.batch_count = 0;
            }

            pub fn runPar(self: *Self, schedule: anytype, world: *World) !void {
                const set = self.schedule_order.getPtr(@intFromEnum(schedule)) orelse return;
                const start = std.Io.Clock.Timestamp.now(world.io, .awake);
                const gpa = world.memtator.frame();
                // Optimized queue entry with dependency tracking
                const QueueEntry = struct {
                    id: SystemID,
                    access: Access(desc.FlagInt),
                };

                // Multi-container architecture for efficient scheduling
                var scheduled_systems = try std.ArrayList(QueueEntry).initCapacity(gpa, 32);
                var batches = try std.ArrayList(std.ArrayList(SystemID)).initCapacity(gpa, 32);
                var active_systems = std.AutoHashMap(SystemID, void).init(gpa);
                var dep_graph = std.AutoHashMap(SystemID, std.ArrayList(SystemID)).init(gpa);
                var dep_counter = std.AutoHashMap(SystemID, usize).init(gpa);

                // prep running system
                for (set.systems.items) |entry| {
                    const sys = self.systems.getPtr(entry.id) orelse continue;
                    const locals = self.locals.getPtr(entry.id) orelse continue;

                    if (sys.condition) |con| {
                        const should_run = con(world, locals) catch {
                            return EcsError.SystemConditionFailure;
                        };

                        if (!should_run) continue;
                    }

                    try scheduled_systems.append(gpa, .{
                        .id = entry.id,
                        .access = sys.access,
                    });
                    try active_systems.put(entry.id, {});
                }

                for (set.systems.items) |entry| {
                    if (!active_systems.contains(entry.id)) continue;
                    if (entry.deps) |deps| {
                        var active_dep_count: usize = 0;
                        for (deps.items) |dep_id| {
                            if (!active_systems.contains(dep_id)) continue;
                            active_dep_count += 1;
                            const res = try dep_graph.getOrPut(dep_id);
                            if (!res.found_existing) res.value_ptr.* = .empty;
                            try res.value_ptr.append(gpa, entry.id);
                        }
                        if (active_dep_count > 0) try dep_counter.put(entry.id, active_dep_count);
                    }
                }

                // prep batches
                while (scheduled_systems.items.len > 0) {
                    var batch: std.ArrayList(SystemID) = .empty;
                    var access = Access(desc.FlagInt){};

                    var remaining: usize = 0;
                    for (scheduled_systems.items) |en| {
                        if ((dep_counter.get(en.id) orelse 0) > 0 or !access.isCompatible(&en.access)) {
                            scheduled_systems.items[remaining] = en;
                            remaining += 1;
                            continue;
                        }

                        try batch.append(gpa, en.id);
                        access.merge(&en.access);
                    }

                    if (batch.items.len == 0) return EcsError.SystemFailure;

                    for (batch.items) |id| {
                        if (dep_graph.getPtr(id)) |deps| {
                            for (deps.items) |dep_id| {
                                if (dep_counter.getPtr(dep_id)) |count| count.* = count.* -| 1;
                            }
                        }
                    }

                    scheduled_systems.items.len = remaining;

                    try batches.append(gpa, batch);
                }

                for (batches.items, 0..) |batch, i| {
                    var group: std.Io.Group = .init;
                    for (batch.items) |id| {
                        const sys = self.systems.getPtr(id).?;
                        const locals = self.locals.getPtr(id).?;
                        std.Io.Group.async(&group, world.io, executeSystem, .{ sys, locals, world, i });
                    }
                    try group.await(world.io);
                }

                const total: i128 = start.untilNow(world.io).raw.toNanoseconds();
                set.run_time_ns = @divTrunc(set.run_time_ns + total, 2);
                set.batch_count = batches.items.len;
            }

            fn executeSystem(sys: *OpaqueSystem, locals: *LocalRegistry(desc.FlagInt), world: *World, batch_id: usize) void {
                const start = std.Io.Clock.Timestamp.now(world.io, .awake);

                sys.run(sys.ptr, world, locals, sys.last_run_tick) catch |err| {
                    std.log.scoped(.knoedel).warn("system failed with: `{any}` @`{s}`", .{ err, sys.debug });
                };

                const total: i128 = start.untilNow(world.io).raw.toNanoseconds();
                sys.run_time_ns = total; // @divTrunc(sys.run_time_ns + total, 2);
                sys.batch_id = batch_id;
                sys.last_run_tick = world.world_tick;
            }
        };

        pub fn And(
            comptime asys: SystemRegistry.ConditionFn,
            comptime bsys: SystemRegistry.ConditionFn,
        ) SystemRegistry.ConditionFn {
            return (struct {
                fn and_con(world: *World, locals: *ResourceRegistry(desc.FlagInt)) EcsError!bool {
                    const a = try asys(world, locals);
                    const b = try bsys(world, locals);
                    return a and b;
                }
            }).and_con;
        }

        pub fn Or(
            comptime asys: SystemRegistry.ConditionFn,
            comptime bsys: SystemRegistry.ConditionFn,
        ) SystemRegistry.ConditionFn {
            return (struct {
                fn or_cond(world: *World, locals: *ResourceRegistry(desc.FlagInt)) EcsError!bool {
                    const a = try asys(world, locals);
                    const b = try bsys(world, locals);
                    return a or b;
                }
            }).or_cond;
        }

        pub fn Res(comptime R: type) type {
            return struct {
                const Self = @This();
                const _read = [1]u32{hashType(R)};
                inner: *const R = undefined,

                pub fn fromWorld(world: *const World) EcsError!Self {
                    var self = Self{};
                    self.inner = try world.resource(R);
                    return self;
                }

                pub inline fn get(self: *Self) *const R {
                    return self.inner;
                }

                pub fn addAccess(world: *World, access: *Access(desc.FlagInt)) void {
                    const flag = world.resources.resource_flags.getFlag(R);
                    access.res_read_write.insert(flag);
                }
            };
        }

        pub fn ResMut(comptime R: type) type {
            return struct {
                const Self = @This();
                const _read = [1]u32{hashType(R)};
                const _write = _read;
                inner: *R = undefined,
                pub fn fromWorld(world: *World) EcsError!Self {
                    var self = Self{};
                    self.inner = try world.resource(R);
                    return self;
                }

                pub inline fn get(self: *Self) *R {
                    return self.inner;
                }

                pub fn addAccess(world: *World, access: *Access(desc.FlagInt)) void {
                    const flag = world.resources.resource_flags.getFlag(R);
                    access.res_write.insert(flag);
                    access.res_read_write.insert(flag);
                }
            };
        }

        // --------------------------------------
        // Jobs pool
        // --------------------------------------
        pub const Jobs = struct {
            const Self = @This();
            io: std.Io,

            pub fn fromWorld(world: *World) EcsError!Self {
                return Jobs{ .io = world.io };
            }

            pub fn go(self: *const Self, group: *std.Io.Group, comptime func: anytype, args: std.meta.ArgsTuple(@TypeOf(func))) EcsError!void {
                std.Io.Group.async(group, self.io, func, args);
            }
        };

        // --------------------------------------
        // Allocation
        // --------------------------------------
        pub const Alloc = struct {
            /// frame arena
            frame: std.mem.Allocator,
            gpa: std.mem.Allocator,
            io: std.Io,

            pub fn fromWorld(world: *World) EcsError!Alloc {
                return .{
                    .frame = world.memtator.frame(),
                    .gpa = world.memtator.parent,
                    .io = world.io,
                };
            }
        };

        // --------------------------------------
        // commands
        // --------------------------------------
        pub const Commands = struct {
            const Self = @This();
            reg: *CommandRegistry,

            /// # Frame Allocator
            /// Arena bound to the lifetime of a single update frame. Leak everything!
            frame_gpa: std.mem.Allocator,

            /// # World Allocator
            /// Arena bound to the liftime of the app.
            world_gpa: std.mem.Allocator,

            /// # Raw World
            /// Unsafe world access.
            world: *World,

            /// checks if the entity is still valid
            pub fn entityValid(self: *const Self, ent: Entity) bool {
                return self.world.isValid(ent);
            }

            /// spawn something new
            /// any tuple in the bundle is spawned as a child
            pub fn spawn(self: *const Self, bundle: anytype) EcsError!Entity {
                const entity = self.world.nextEntityId();
                const cmd = try insertCommand(self.frame_gpa, entity, bundle);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
                return entity;
            }

            /// spawns an empty entity
            pub fn spawnEmpty(self: *const Self) Entity {
                return self.world.nextEntityId();
            }

            /// spawn with a specific pre-claimed entity id
            pub fn spawnWithEntity(self: *const Self, entity: Entity, bundle: anytype) EcsError!void {
                const cmd = try insertCommand(self.frame_gpa, entity, bundle);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// despawns an entity with children recursive
            pub fn despawn(self: *const Self, entity: Entity) EcsError!void {
                const cmd = try despawnCommand(self.frame_gpa, entity, true);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// despawns an entity and unlink their children without despawning them
            pub fn despawnUnlink(self: *const Self, entity: Entity) EcsError!void {
                const cmd = try despawnCommand(self.frame_gpa, entity, false);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// remove one or many components from entity
            /// allowes type or tuple of types.
            pub fn remove(self: *const Self, entity: Entity, comptime C: anytype) EcsError!void {
                const Info = @typeInfo(@TypeOf(C));

                switch (Info) {
                    .@"struct" => |str| {
                        if (!str.is_tuple) @compileError("Components to be removed must be single type or tuple of types");

                        const cmd = try removeBundleCommand(self.frame_gpa, entity, C);
                        try self.reg.add(self.world.io, self.world_gpa, cmd);
                    },
                    else => {
                        const cmd = try removeCommand(self.frame_gpa, entity, C);
                        try self.reg.add(self.world.io, self.world_gpa, cmd);
                    },
                }
            }

            /// add a subcommand
            pub fn add(self: *const Self, cmd: Command) EcsError!void {
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// add a component to entity, overwritting existing
            pub fn insert(self: *const Self, entity: Entity, comp: anytype) EcsError!void {
                const cmd = try insertCommand(self.frame_gpa, entity, comp);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// Insert raw component bytes into an entity by flag. Used for deserialization.
            pub fn insertRaw(self: *const Self, entity: Entity, flag: HeapFlagSet(desc.FlagInt).Flag, bytes: []const u8) EcsError!void {
                const cmd = try insertRawCommand(self.frame_gpa, entity, flag, bytes);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// remove a component from an entity by runtime flag
            pub fn removeRaw(self: *const Self, entity: Entity, flag: HeapFlagSet(desc.FlagInt).Flag) EcsError!void {
                const cmd = try removeRawCommand(self.frame_gpa, entity, flag);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            /// add a resource, overwritting existing (calls `deinit` with alloc on res)
            pub fn insertResource(self: *const Self, comp: anytype) EcsError!void {
                const cmd = try insertResourceCommand(self.frame_gpa, comp);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            pub fn removeResource(self: *const Self, comp: anytype) EcsError!void {
                const cmd = try removeResourceCommand(self.frame_gpa, comp);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            pub fn addChild(self: *const Self, parent: Entity, child: Entity) EcsError!void {
                const cmd = try addChildCommand(self.frame_gpa, parent, child);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            pub fn removeChild(self: *const Self, parent: Entity, child: Entity) EcsError!void {
                const cmd = try removeChildCommand(self.frame_gpa, parent, child);
                try self.reg.add(self.world.io, self.world_gpa, cmd);
            }

            pub fn fromWorld(world: *World) EcsError!Self {
                return Self{
                    .reg = &world.commands,
                    .world = world,
                    .world_gpa = world.memtator.world(),
                    .frame_gpa = world.memtator.frame(),
                };
            }
        };

        pub const CommandRegistry = struct {
            const Self = @This();

            mutex: std.Io.Mutex = .init,
            queue: std.ArrayList(Command) = .empty,

            /// Queue storage must use the world allocator; payloads may use the frame allocator.
            pub fn add(self: *Self, io: std.Io, allocator: std.mem.Allocator, cmd: Command) EcsError!void {
                try self.mutex.lock(io);
                defer self.mutex.unlock(io);

                try self.queue.append(allocator, cmd);
            }

            pub fn runAllUnsafe(self: *Self, world: *World) void {
                var i: usize = 0;
                while (i < self.queue.items.len) {
                    const cmd = self.queue.items[i];
                    cmd.run(cmd.ptr, world) catch |err| {
                        std.log.scoped(.knoedel).warn("Command failed to run with `{any}`", .{err});
                    };

                    i += 1;
                }
                self.queue.clearRetainingCapacity();
            }
        };

        fn removeResourceCommand(comptime R: type) EcsError!Command {
            return Command{
                .ptr = undefined,
                .run = (struct {
                    fn run(_: *anyopaque, world: *World) EcsError!void {
                        try world.resources.remove(world.memtator.world(), R);
                    }
                }).run,
            };
        }

        fn addChildCommand(allocator: std.mem.Allocator, parent: Entity, child: Entity) EcsError!Command {
            const Args = struct {
                parent: Entity,
                child: Entity,
            };

            var arg_ptr = try allocator.create(Args);
            arg_ptr.parent = parent;
            arg_ptr.child = child;

            return Command{
                .ptr = arg_ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const gpa = world.memtator.world();
                        const args: *Args = @ptrCast(@alignCast(ctx));

                        if (world.components.getSingleAndUpdate(world.world_tick, args.parent, Children)) |c| {
                            try c.items.append(gpa, args.child);
                        } else {
                            var c = Children{};
                            try c.items.append(gpa, args.child);
                            try world.components.add(gpa, world.world_tick, args.parent, c);
                        }

                        try world.components.add(gpa, world.world_tick, args.child, Parent{ .entity = args.parent });
                    }
                }).run,
            };
        }

        fn removeChildCommand(allocator: std.mem.Allocator, parent: Entity, child: Entity) EcsError!Command {
            const Args = struct {
                parent: Entity,
                child: Entity,
            };

            var arg_ptr = try allocator.create(Args);
            arg_ptr.parent = parent;
            arg_ptr.child = child;

            return Command{
                .ptr = arg_ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const gpa = world.memtator.world();
                        const args: *Args = @ptrCast(@alignCast(ctx));

                        const remov_cmd = try removeCommand(gpa, args.child, Parent);
                        try remov_cmd.run(remov_cmd.ptr, world);

                        if (world.components.getSingleAndUpdate(world.world_tick, args.parent, Children)) |children| {
                            var index: ?usize = null;
                            for (children.items.items, 0..) |c, i| {
                                if (c == args.child) index = i;
                            }
                            if (index) |i| _ = children.items.swapRemove(i);
                        }
                    }
                }).run,
            };
        }

        fn insertResourceCommand(allocator: std.mem.Allocator, res: anytype) EcsError!Command {
            const ResourceType = @TypeOf(res);
            const ptr = try allocator.create(ResourceType);
            ptr.* = res;

            return Command{
                .ptr = ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const res_ptr: *ResourceType = @ptrCast(@alignCast(ctx));
                        try world.addResource(res_ptr.*);
                    }
                }).run,
            };
        }

        fn despawnCommand(allocator: std.mem.Allocator, entity: Entity, comptime with_children: bool) EcsError!Command {
            const ptr = try allocator.create(Entity);
            ptr.* = entity;

            return Command{
                .ptr = ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const ent: *Entity = @ptrCast(@alignCast(ctx));
                        try world.despawn(ent.*, with_children);
                    }
                }).run,
            };
        }

        fn removeCommand(allocator: std.mem.Allocator, entity: Entity, comptime C: type) EcsError!Command {
            const ptr = try allocator.create(Entity);
            ptr.* = entity;

            return Command{
                .ptr = ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const ent: *Entity = @ptrCast(@alignCast(ctx));
                        if (world.isValid(ent.*)) {
                            const flag = world.components.component_flags.getFlag(C);

                            if (world.hooks.remove_hooks.contains(flag)) {
                                const comp = world.components.getSingle(ent.*, C) orelse return;
                                try world.hooks.runRemoveHook(flag, comp, ent.*, world);
                            }

                            try world.components.remove(world.memtator.world(), ent.*, C);
                        }
                    }
                }).run,
            };
        }

        fn removeBundleCommand(allocator: std.mem.Allocator, entity: Entity, comptime B: anytype) EcsError!Command {
            const ptr = try allocator.create(Entity);
            ptr.* = entity;

            return Command{
                .ptr = ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const ent: *Entity = @ptrCast(@alignCast(ctx));
                        if (world.isValid(ent.*)) {
                            inline for (B) |CompType| {
                                const flag = world.components.component_flags.getFlag(CompType);

                                if (world.hooks.remove_hooks.contains(flag)) {
                                    const comp = world.components.getSingle(ent.*, CompType) orelse return;
                                    try world.hooks.runRemoveHook(flag, comp, ent.*, world);
                                }

                                try world.components.remove(world.memtator.world(), ent.*, CompType);
                            }
                        }
                    }
                }).run,
            };
        }

        fn insertRawCommand(allocator: std.mem.Allocator, entity: Entity, flag: HeapFlagSet(desc.FlagInt).Flag, bytes: []const u8) EcsError!Command {
            const CompFlag = HeapFlagSet(desc.FlagInt).Flag;
            const Args = struct {
                ent: Entity,
                flag: CompFlag,
                data: []const u8,
            };

            const args = try allocator.create(Args);
            // copy bytes to frame allocator so they outlive the caller's buffer
            const data_copy = try allocator.alloc(u8, bytes.len);
            @memcpy(data_copy, bytes);
            args.* = .{ .ent = entity, .flag = flag, .data = data_copy };

            return Command{
                .ptr = args,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const a: *Args = @ptrCast(@alignCast(ctx));
                        if (world.isValid(a.ent)) {
                            try world.components.addRaw(world.memtator.world(), world.world_tick, a.ent, a.flag, a.data);
                        }
                    }
                }).run,
            };
        }

        fn removeRawCommand(allocator: std.mem.Allocator, entity: Entity, flag: HeapFlagSet(desc.FlagInt).Flag) EcsError!Command {
            const CompFlag = HeapFlagSet(desc.FlagInt).Flag;
            const Args = struct {
                ent: Entity,
                flag: CompFlag,
            };

            const args = try allocator.create(Args);
            args.* = .{ .ent = entity, .flag = flag };

            return Command{
                .ptr = args,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const a: *Args = @ptrCast(@alignCast(ctx));
                        if (!world.isValid(a.ent)) return;
                        if (world.hooks.remove_hooks.contains(a.flag)) {
                            if (world.components.getSingleOpaque(a.ent, a.flag)) |comp| {
                                try world.hooks.runRemoveHook(a.flag, comp, a.ent, world);
                            }
                        }
                        try world.components.removeByFlag(world.memtator.world(), a.ent, a.flag);
                    }
                }).run,
            };
        }

        fn insertCommand(allocator: std.mem.Allocator, entity: Entity, bundle: anytype) EcsError!Command {
            if (@typeInfo(@TypeOf(bundle)) != .@"struct") @compileError("a bundle must be a tuple struct of components");

            const ArgType = @TypeOf(bundle);
            const Args = struct {
                ent: Entity,
                bundle: ArgType,
            };

            var arg_ptr = try allocator.create(Args);
            arg_ptr.bundle = bundle;
            arg_ptr.ent = entity;

            return Command{
                .ptr = arg_ptr,
                .run = (struct {
                    fn run(ctx: *anyopaque, world: *World) EcsError!void {
                        const gpa = world.memtator.world();
                        const args: *Args = @ptrCast(@alignCast(ctx));

                        if (!isTuple(ArgType)) {
                            switch (@typeInfo(@TypeOf(bundle))) {
                                .@"struct" => {},
                                .@"enum" => {},
                                .@"union" => {},
                                else => @compileError("component in insert must be of type struct/enum/union, `" ++ @typeName(@TypeOf(bundle)) ++ "` given"),
                            }

                            const flag = world.components.component_flags.getFlag(@TypeOf(bundle));
                            try world.hooks.runAddedHook(flag, &args.bundle, args.ent, world);
                            try world.components.add(world.memtator.world(), world.world_tick, args.ent, args.bundle);
                            return;
                        }

                        inline for (args.bundle, 0..) |comp, i| {
                            const CompType: type = @TypeOf(comp);

                            switch (@typeInfo(CompType)) {
                                .@"struct" => {},
                                .@"enum" => {},
                                .@"union" => {},
                                else => @compileError("component in bundle must be of type struct/enum/union, `" ++ @typeName(CompType) ++ "` given"),
                            }

                            // ---------------------
                            // spawn children
                            if (isTuple(CompType)) {
                                const child_ent = world.nextEntityId();
                                const cmp = try insertCommand(world.memtator.frame(), child_ent, comp);
                                try cmp.run(cmp.ptr, world);

                                if (world.components.getSingle(args.ent, Children)) |c| {
                                    try c.items.append(gpa, child_ent);
                                } else {
                                    var c = Children{};
                                    try c.items.append(gpa, child_ent);
                                    try world.components.add(world.memtator.world(), world.world_tick, args.ent, c);
                                }

                                try world.components.add(world.memtator.world(), world.world_tick, child_ent, Parent{ .entity = args.ent });
                            } else {

                                // hooks
                                const info = @typeInfo(ArgType);
                                if (!info.@"struct".field_attrs[i].@"comptime") {
                                    const flag = world.components.component_flags.getFlag(CompType);
                                    try world.hooks.runAddedHook(flag, &args.bundle[i], args.ent, world);
                                }

                                // required comps
                                if (@hasDecl(CompType, "Required")) {
                                    inline for (CompType.Required) |req| {
                                        const ReqType = @TypeOf(req);
                                        const ReqInfo = @typeInfo(ReqType);

                                        switch (ReqInfo) {
                                            .@"struct" => {},
                                            .@"enum" => {},
                                            .@"union" => {},
                                            else => |ty| @compileError("Required Component must be struct or enum, found " ++ @tagName(ty)),
                                        }
                                    }

                                    try world.components.addBundle(world.memtator.world(), world.world_tick, args.ent, CompType.Required);
                                }
                            }
                            // ---------------------
                        }

                        try world.components.addBundle(world.memtator.world(), world.world_tick, args.ent, args.bundle);
                    }
                }).run,
            };
        }

        pub const CommandFn = *const fn (ctx: *anyopaque, world: *World) EcsError!void;

        pub const Command = struct {
            ptr: *anyopaque,
            run: CommandFn,
        };

        pub fn QueryF(comptime Q: type, filter: Filter) type {
            return IQueryStructFilteredNew(desc, Q, filter);
        }

        pub fn Query(comptime Q: type) type {
            return IQueryStructFilteredNew(desc, Q, .empty);
        }
    };
}

const ArchEntry = struct {
    arch_id: u32,
    set_id: u32,
};

/// cached querry values, that only need to compute once
fn QueryState(FlagInt: type, comptime Q: type, comptime F: Filter) type {
    return struct {
        const Self = @This();
        const FlagSet = HeapFlagSet(FlagInt);
        // ---------------
        created_on: ?u64 = null,
        access_sets: [F.BranchCount()]AccessSet(FlagInt) = undefined,
        matched_archtypes: std.ArrayList(ArchEntry) = .empty,
        last_update: u32 = 0,

        pub fn new(
            gpa: std.mem.Allocator,
            flags: *FlagSet,
            archtypes: []const ArchType(FlagInt),
            tick: u32,
        ) !Self {
            var state = Self{};

            state.build_access_set(flags);
            try state.build_match(gpa, archtypes);

            state.last_update = @intCast(archtypes.len);
            state.created_on = tick;
            return state;
        }

        pub fn build_access_set(self: *Self, flags: *FlagSet) void {
            var include = FlagSet.Set.empty;

            const QueryInfo = @typeInfo(Q);
            inline for (QueryInfo.@"struct".field_types) |f_type| {
                const info = @typeInfo(f_type);
                switch (info) {
                    .pointer => |ptr| {
                        const comp_id = flags.getFlag(ptr.child);
                        include.insert(comp_id);
                    },
                    .@"enum", .@"struct", .optional => {},
                    else => @compileError("not allowed"),
                }
            }

            self.access_sets = try F.accessSets(FlagInt, flags, .{
                .with = include,
            });
        }

        pub fn build_match(self: *Self, gpa: std.mem.Allocator, arches: []const ArchType(FlagInt)) !void {
            self.matched_archtypes.clearRetainingCapacity();
            blk: for (arches, 0..) |*arch, i| {
                for (self.access_sets, 0..) |access, s| {
                    if (access.matches(arch.mask)) {
                        try self.matched_archtypes.append(gpa, .{
                            .arch_id = @intCast(i),
                            .set_id = @intCast(s),
                        });
                        continue :blk;
                    }
                }
            }
        }

        pub fn deinit(self: *Self, gpa: std.mem.Allocator) void {
            self.matched_archtypes.deinit(gpa);
        }
    };
}

fn AccessSet(FlagInt: type) type {
    return struct {
        with: HeapFlagSet(FlagInt).Set = .empty,
        without: HeapFlagSet(FlagInt).Set = .empty,
        added: HeapFlagSet(FlagInt).Set = .empty,
        changed: HeapFlagSet(FlagInt).Set = .empty,

        pub fn matches(self: *const @This(), other: HeapFlagSet(FlagInt).Set) bool {
            if (!self.with.intersectWith(other).eql(self.with)) return false;
            return self.without.intersectWith(other).eql(.empty);
        }
    };
}

pub const Filter = union(enum) {
    with: u32,
    without: u32,
    added: u32,
    changed: u32,
    @"and": []const Filter,
    @"or": []const Filter,
    empty,
    // --------------------------
    pub const Branch = struct { a: *const Filter, b: *const Filter };
    pub fn nodeCount(comptime self: *const Filter) u32 {
        switch (self.*) {
            .with, .without, .added, .changed => return 1,
            .@"and", .@"or" => |children| {
                var sum: u32 = 0;
                for (children) |child| sum += child.nodeCount();
                return sum;
            },
        }
    }

    /// the dnf count
    pub fn BranchCount(comptime self: *const Filter) u32 {
        switch (self.*) {
            .with, .without, .added, .changed, .empty => return 1,
            .@"and" => |children| {
                var product: u32 = 1;
                for (children) |child| product *= child.BranchCount();
                return product;
            },
            .@"or" => |children| {
                var sum: u32 = 0;
                for (children) |child| sum += child.BranchCount();
                return sum;
            },
        }
    }

    pub fn IsArchOnly(comptime self: *const Filter) bool {
        switch (self.*) {
            .with, .without, .empty => return true,
            .added, .changed => return false,
            .@"or", .@"and" => |children| {
                for (children) |f| if (!f.IsArchOnly()) return false;
                return true;
            },
        }
    }

    const TickHashes = struct {
        added: []const u32,
        changed: []const u32,
    };

    /// Returns per-branch added/changed hashes, computed entirely at comptime.
    pub fn comptimeBranchTicks(comptime self: *const Filter) [self.BranchCount()]TickHashes {
        var out: [self.BranchCount()]TickHashes = undefined;
        for (&out) |*o| {
            o.added = &.{};
            o.changed = &.{};
        }
        _ = self.collectTickBranches(out[0..], 0, &.{}, &.{});
        return out;
    }

    fn collectTickBranches(
        comptime self: *const Filter,
        out: []TickHashes,
        comptime offset: usize,
        comptime inherited_added: []const u32,
        comptime inherited_changed: []const u32,
    ) usize {
        switch (self.*) {
            .added => |hash| {
                out[offset] = .{
                    .added = inherited_added ++ &[_]u32{hash},
                    .changed = inherited_changed,
                };
                return offset + 1;
            },
            .changed => |hash| {
                out[offset] = .{
                    .added = inherited_added,
                    .changed = inherited_changed ++ &[_]u32{hash},
                };
                return offset + 1;
            },
            .with, .without => {
                out[offset] = .{
                    .added = inherited_added,
                    .changed = inherited_changed,
                };
                return offset + 1;
            },
            .@"and" => |children| {
                return andTickBranches(children, 0, out, offset, inherited_added, inherited_changed);
            },
            .@"or" => |children| {
                var off = offset;
                inline for (children) |child| {
                    off = child.collectTickBranches(out, off, inherited_added, inherited_changed);
                }
                return off;
            },
            .empty => return offset,
        }
    }

    fn andTickBranches(
        comptime children: []const Filter,
        comptime idx: usize,
        out: anytype,
        comptime offset: usize,
        comptime inherited_added: []const u32,
        comptime inherited_changed: []const u32,
    ) usize {
        if (comptime idx >= children.len) {
            out[offset] = .{
                .added = inherited_added,
                .changed = inherited_changed,
            };
            return offset + 1;
        }
        const child = comptime &children[idx];
        comptime var child_branches: [child.BranchCount()]TickHashes = undefined;
        for (&child_branches) |*cb| {
            cb.added = &.{};
            cb.changed = &.{};
        }
        _ = child.collectTickBranches(child_branches[0..], 0, inherited_added, inherited_changed);
        var off = offset;
        inline for (0..comptime child.BranchCount()) |i| {
            off = andTickBranches(children, idx + 1, out, off, child_branches[i].added, child_branches[i].changed);
        }
        return off;
    }

    // Creates a DNF representation: one AccessSet per OR-branch.
    // Each AccessSet holds the flags that must be present (`with`) and absent (`without`).
    pub fn accessSets(
        comptime self: *const Filter,
        comptime FlagInt: type,
        flags: *HeapFlagSet(FlagInt),
        inherited: AccessSet(FlagInt),
    ) ![self.BranchCount()]AccessSet(FlagInt) {
        var out: [self.BranchCount()]AccessSet(FlagInt) = undefined;
        _ = try self.collectBranches(FlagInt, flags, out[0..], 0, inherited);
        return out;
    }

    // Fills `out[offset..]` with one AccessSet per OR-branch rooted at `self`.
    // `inherited` carries flags accumulated by enclosing AND nodes.
    // Returns the next free offset.
    fn collectBranches(
        comptime self: *const Filter,
        comptime FlagInt: type,
        flags: *HeapFlagSet(FlagInt),
        out: []AccessSet(FlagInt),
        offset: usize,
        inherited: AccessSet(FlagInt),
    ) !usize {
        switch (self.*) {
            .added => |hash| {
                var s = inherited;
                if (flags.getFlagFromHash(hash)) |flag| {
                    s.with.insert(flag);
                    s.added.insert(flag);
                } else {
                    s.without = .full;
                }
                out[offset] = s;
                return offset + 1;
            },
            .changed => |hash| {
                var s = inherited;
                if (flags.getFlagFromHash(hash)) |flag| {
                    s.with.insert(flag);
                    s.changed.insert(flag);
                } else {
                    s.without = .full;
                }

                out[offset] = s;
                return offset + 1;
            },
            .with => |hash| {
                var s = inherited;
                if (flags.getFlagFromHash(hash)) |flag| {
                    s.with.insert(flag);
                } else {
                    s.without = .full;
                }
                out[offset] = s;
                return offset + 1;
            },
            .without => |hash| {
                var s = inherited;
                if (flags.getFlagFromHash(hash)) |flag| {
                    s.without.insert(flag);
                }
                out[offset] = s;
                return offset + 1;
            },
            .@"and" => |children| {
                return try andBranches(children, 0, FlagInt, flags, out, offset, inherited);
            },
            .@"or" => |children| {
                var off = offset;
                inline for (children) |child| {
                    off = try child.collectBranches(FlagInt, flags, out, off, inherited);
                }
                return off;
            },
            .empty => {
                out[offset] = inherited;
                return offset + 1;
            },
        }
    }

    // Computes the cross-product of `children[idx..]` branches into `out[offset..]`.
    // Each child's branches are collected with `inherited` as the starting point; the
    // next child then recurses with each of those results as its new `inherited`.
    fn andBranches(
        comptime children: []const Filter,
        comptime idx: usize,
        comptime FlagInt: type,
        flags: *HeapFlagSet(FlagInt),
        out: []AccessSet(FlagInt),
        offset: usize,
        inherited: AccessSet(FlagInt),
    ) !usize {
        if (comptime idx >= children.len) {
            out[offset] = inherited;
            return offset + 1;
        }
        const child = comptime &children[idx];
        var child_branches: [child.BranchCount()]AccessSet(FlagInt) = undefined;
        _ = try child.collectBranches(FlagInt, flags, child_branches[0..], 0, inherited);
        var off = offset;
        inline for (0..comptime child.BranchCount()) |i| {
            off = try andBranches(children, idx + 1, FlagInt, flags, out, off, child_branches[i]);
        }
        return off;
    }

    pub fn With(comptime T: type) Filter {
        return .{ .with = comptime hashType(T) };
    }

    pub fn Added(comptime T: type) Filter {
        return .{ .added = comptime hashType(T) };
    }

    pub fn Changed(comptime T: type) Filter {
        return .{ .changed = comptime hashType(T) };
    }

    pub fn Without(comptime T: type) Filter {
        return .{ .without = comptime hashType(T) };
    }

    pub fn And(comptime a: Filter, comptime b: Filter) Filter {
        return .{ .@"and" = &.{ a, b } };
    }

    pub fn And3(comptime a: Filter, comptime b: Filter, comptime c: Filter) Filter {
        return .{ .@"and" = &.{ a, b, c } };
    }

    pub fn Or(comptime a: Filter, comptime b: Filter) Filter {
        return .{ .@"or" = &.{ a, b } };
    }

    pub fn Or3(comptime a: Filter, comptime b: Filter, comptime c: Filter) Filter {
        return .{ .@"or" = &.{ a, b, c } };
    }
};

pub fn Local(comptime T: type) type {
    return struct {
        inner: *T = undefined,

        const Self = @This();
        pub const is_local_marker: bool = true;
        const innerType = T;

        pub inline fn get(self: *Self) *T {
            return self.inner;
        }
    };
}

pub fn Has(comptime T: type) type {
    return struct {
        pub const _is_has: bool = true;
        const inner = T;
        val: bool = false,
    };
}

/// # expects tuple. Systems run in order. Tuple in tuple run in par!
pub fn Chain(comptime T: anytype) type {
    const ty = @TypeOf(T);
    if (!isTuple(ty)) @compileLog("`Chain` expects a tuple");

    return struct {
        pub const _is_chain: bool = true;
        const inner: @TypeOf(T) = T;
    };
}

pub inline fn hashStr(str: []const u8) u32 {
    @setEvalBranchQuota(3200);
    var value: u32 = 2166136261;
    inline for (str) |c| value = (value ^ @as(u32, @intCast(c))) *% 16777619;
    return value;
}

pub inline fn hashType(comptime T: type) u32 {
    @setEvalBranchQuota(3200);
    var value: u32 = 2166136261;
    for (@typeName(T)) |c| value = (value ^ @as(u32, @intCast(c))) *% 16777619;
    return value;
}

pub inline fn isTuple(comptime T: type) bool {
    switch (@typeInfo(T)) {
        .@"struct" => |s| return s.is_tuple,
        else => return false,
    }
}

// ---------------------------------------
// resource
// ---------------------------------------
const Resource = struct {
    ctx: *anyopaque,
    deinit: *const fn (ptr: *anyopaque, alloc: std.mem.Allocator) void,

    pub fn cast(ptr: *anyopaque, comptime T: type) *T {
        return @ptrCast(@alignCast(ptr));
    }

    pub fn init(res: anytype, alloc: std.mem.Allocator) Resource {
        const ptr = alloc.create(@TypeOf(res)) catch @panic("oom");
        ptr.* = res;
        return Resource{
            .ctx = ptr,
            .deinit = (struct {
                fn deinit(p: *anyopaque, allocator: std.mem.Allocator) void {
                    var r = Resource.cast(p, @TypeOf(res));
                    if (@hasDecl(@TypeOf(res), "deinit")) {
                        r.deinit(allocator);
                    }
                    allocator.destroy(r);
                }
            }).deinit,
        };
    }
};

pub fn ResourceRegistry(FlagInt: type) type {
    return struct {
        const ResID = u32;
        const TypeID = u32;
        const Self = @This();

        data: std.AutoHashMapUnmanaged(TypeID, Resource) = .empty,
        resource_flags: HeapFlagSet(FlagInt) = .{},
        /// resource hash -> codec
        codecs: std.AutoHashMapUnmanaged(u32, ResourceCodec) = .empty,

        pub const ResourceCodec = struct {
            name: []const u8,
            serialize: *const fn (*const anyopaque, *std.Io.Writer) anyerror!void,
            deserializeRegister: *const fn (*Self, std.mem.Allocator, *std.Io.Reader) anyerror!void,
        };

        pub fn deinit(self: *Self, gpa: std.mem.Allocator) void {
            var resourceIterator = self.data.iterator();
            while (resourceIterator.next()) |entry| entry.value_ptr.deinit(entry.value_ptr.ctx, gpa);
            self.data.deinit(gpa);
            self.codecs.deinit(gpa);
        }

        pub fn get(self: *const Self, comptime T: type) ?*T {
            const hash = hashType(T);
            const res = self.data.get(hash) orelse return null;
            return Resource.cast(res.ctx, T);
        }

        pub fn registerCodec(self: *Self, allocator: std.mem.Allocator, comptime R: type, codec: ResourceCodec) !void {
            _ = self.resource_flags.getFlag(R);
            try self.codecs.put(allocator, hashType(R), codec);
        }

        pub fn getOrDefault(self: *Self, gpa: std.mem.Allocator, comptime T: type) !*T {
            const hash = hashType(T);
            const entry = try self.data.getOrPut(gpa, hash);
            if (!entry.found_existing) entry.value_ptr.* = Resource.init(T{}, gpa);
            return Resource.cast(entry.value_ptr.ctx, T);
        }

        pub fn remove(self: *Self, gpa: std.mem.Allocator, comptime R: type) !void {
            const hash = hashType(R);
            const entry = self.data.fetchRemove(hash) orelse return;
            entry.value.deinit(entry.value.ctx, gpa);
        }

        pub fn tryRegister(self: *Self, gpa: std.mem.Allocator, resource: anytype) !void {
            const ResType = @TypeOf(resource);
            const hash = hashType(ResType);

            if (self.data.contains(hash)) return;
            try self.data.put(gpa, hash, Resource.init(resource, gpa));
        }

        pub fn register(self: *Self, gpa: std.mem.Allocator, resource: anytype) !void {
            const ResType = @TypeOf(resource);
            const hash = hashType(ResType);
            const entry = try self.data.getOrPut(gpa, hash);
            if (entry.found_existing) {
                entry.value_ptr.deinit(entry.value_ptr.ctx, gpa);
            }
            entry.value_ptr.* = Resource.init(resource, gpa);
        }
    };
}

pub fn HeapFlagSet(comptime FlagInt: type) type {
    return struct {
        const Self = @This();
        const Max: usize = @as(usize, std.math.maxInt(FlagInt)) + 1;
        pub const Flag = enum(FlagInt) { _ };
        pub const Set = std.enums.EnumSet(Flag);

        registration_mutex: std.atomic.Mutex = .unlocked,
        registered_hash: [Max]u32 = @splat(0),
        registered_buf: [Max]Info = undefined,
        registered_len: usize = 0,

        pub const Info = struct {
            name: []const u8,
            hash: u32,
            size: usize,
            alignment: usize,
            print: ?*const fn (*const anyopaque, *std.Io.Writer) EcsError!void,
            /// Comptime-generated JSON mapping (write/schema/apply), null when
            /// no field of the type is JSON-representable.
            json: ?*const json_codec.JsonVTable = null,
        };

        pub inline fn getFlagFromHash(self: *const Self, hash: u32) ?Flag {
            const len = @atomicLoad(usize, &self.registered_len, .acquire);
            for (self.registered_hash[0..len], 0..) |h, i| if (h == hash) return @enumFromInt(i);
            return null;
        }

        pub fn getFlag(self: *Self, comptime T: type) Flag {
            const hash = hashType(T);

            const len = @atomicLoad(usize, &self.registered_len, .acquire);
            for (self.registered_hash[0..len], 0..) |h, i| {
                if (h == hash) {
                    self.refreshAfterReload(i, T);
                    return @enumFromInt(i);
                }
            }

            while (!self.registration_mutex.tryLock()) std.atomic.spinLoopHint();
            defer self.registration_mutex.unlock();

            const locked_len = @atomicLoad(usize, &self.registered_len, .acquire);
            for (self.registered_hash[0..locked_len], 0..) |h, i| {
                if (h == hash) {
                    self.refreshAfterReload(i, T);
                    return @enumFromInt(i);
                }
            }

            const index = locked_len;
            assert(index < Max);

            self.registered_hash[index] = hash;
            self.registered_buf[index] = .{
                .name = @typeName(T),
                .hash = hash,
                .size = @sizeOf(T),
                .alignment = @alignOf(T),
                .print = comptime makePrintFn(T),
                .json = comptime makeJsonPtr(T),
            };

            @atomicStore(usize, &self.registered_len, index + 1, .release);
            return @enumFromInt(index);
        }

        /// Called on every name-hash match. After a hot reload the world
        /// persists but the old dylib text is stale: refresh the fn pointers.
        /// A layout change cannot be repaired in place, existing archetype
        /// columns keep the old size, so fail fast instead of corrupting.
        fn refreshAfterReload(self: *Self, index: usize, comptime T: type) void {
            const info = &self.registered_buf[index];
            if (info.size != @sizeOf(T) or info.alignment != @alignOf(T)) {
                std.debug.panic(
                    "component `{s}` layout changed across hot reload ({d}/{d} -> {d}/{d} size/align). Restart the game.",
                    .{ info.name, info.size, info.alignment, @sizeOf(T), @alignOf(T) },
                );
            }
            // Benign race — both old and new pointers stay valid.
            info.print = comptime makePrintFn(T);
            info.json = comptime makeJsonPtr(T);
        }

        fn makePrintFn(comptime T: type) ?*const fn (*const anyopaque, *std.Io.Writer) EcsError!void {
            return (struct {
                pub fn fmt(ptr: *const anyopaque, w: *std.Io.Writer) EcsError!void {
                    const comp: *const T = @ptrCast(@alignCast(ptr));
                    if (@hasDecl(T, "fmt")) {
                        try comp.fmt(w);
                    } else {
                        switch (@typeInfo(T)) {
                            .@"struct" => |str| {
                                inline for (str.field_names, 0..) |f_name, i| {
                                    try w.print(".{s}: {any},", .{ f_name, @field(comp, f_name) });
                                    if (i < str.field_names.len -| 1) try w.print("\n", .{});
                                }
                            },
                            else => try w.writeAll("uknown"),
                        }
                    }
                }
            }).fmt;
        }

        fn makeJsonPtr(comptime T: type) ?*const json_codec.JsonVTable {
            const Impl = json_codec.Codec(T) orelse return null;
            const default_ok = comptime json_codec.defaultable(T);
            return &(json_codec.JsonVTable{
                .write = Impl.write,
                .schema = Impl.schema,
                .apply = Impl.apply,
                .init = if (default_ok) &Impl.init else null,
            });
        }

        pub fn getId(self: *const Self, flag: Flag) *const Info {
            assert(@intFromEnum(flag) < self.registered_len);
            return &self.registered_buf[@intFromEnum(flag)];
        }
    };
}

/// access flags for lock free concurrency
pub fn Access(FlagInt: type) type {
    const FlagSet = HeapFlagSet(FlagInt);
    return struct {
        const Self = @This();
        comp_read_write: FlagSet.Set = .{},
        comp_write: FlagSet.Set = .{},
        res_read_write: FlagSet.Set = .{},
        res_write: FlagSet.Set = .{},

        pub fn isCompatible(self: *const Self, lhs: *const Self) bool {
            return self.comp_compatible(lhs) and self.res_compatible(lhs);
        }

        fn merge(self: *Self, lhs: *const Self) void {
            self.comp_read_write.setUnion(lhs.comp_read_write);
            self.comp_write.setUnion(lhs.comp_write);
            self.res_read_write.setUnion(lhs.res_read_write);
            self.res_write.setUnion(lhs.res_write);
        }

        inline fn comp_compatible(self: *const Self, other: *const Self) bool {
            const empty = FlagSet.Set.empty;
            if (!self.comp_read_write.intersectWith(other.comp_write).eql(empty)) return false;
            if (!self.comp_write.intersectWith(other.comp_read_write).eql(empty)) return false;
            return true;
        }

        inline fn res_compatible(self: *const Self, other: *const Self) bool {
            const empty = FlagSet.Set.empty;
            if (!self.res_read_write.intersectWith(other.res_write).eql(empty)) return false;
            if (!self.res_write.intersectWith(other.res_read_write).eql(empty)) return false;
            return true;
        }
    };
}

/// ArchType = opaque runtime MultiArrayList
pub fn ArchType(FlagInt: type) type {
    return struct {
        const Self = @This();
        const CompFlag = HeapFlagSet(FlagInt).Flag;
        const ColMeta = struct {
            flag: CompFlag,
            offset: usize,
            size: usize,
            hash: u32,
        };
        pub const TickInfo = struct {
            added: u32 = 0,
            changed: u32 = 0,
        };

        /// Count fields in a query struct that need a cached meta pointer
        pub fn queryMetaCount(comptime Q: type) usize {
            const info = @typeInfo(Q).@"struct";
            comptime var count: usize = 0;
            inline for (info.field_types) |f_type| {
                if (f_type == Entity or f_type == Meta or @sizeOf(f_type) == 0) continue;
                count += 1;
            }
            return count;
        }

        /// Entity Meta information
        /// fast `has` checks on components
        pub const Meta = struct {
            _columns: []const ColMeta,
            _mask: *const HeapFlagSet(FlagInt).Set,

            pub fn has(self: @This(), comptime T: type) bool {
                const target = comptime hashType(T);
                for (self._columns) |col| {
                    if (col.hash == target) return true;
                }
                return false;
            }

            pub fn columns(self: @This()) []const ColMeta {
                return self._columns;
            }

            pub fn mask(self: @This()) *const HeapFlagSet(FlagInt).Set {
                return self._mask;
            }
        };
        /// allocate in chunks of:
        chunk_size: usize = 512,
        mask: HeapFlagSet(FlagInt).Set = .empty,
        bytes: []u8 = &.{},
        alignment: usize = 0,
        allocated_table_alignment: std.mem.Alignment = .fromByteUnits(16),
        columns: std.ArrayList(ColMeta) = .empty,
        entity_lookup: std.AutoHashMapUnmanaged(Entity, usize) = .empty,
        len: usize = 0,
        capacity: usize = 0,

        inline fn effectiveBaseAlignment(self: *const Self) usize {
            if (self.alignment == 0) return @alignOf(Entity);
            return self.alignment;
        }

        inline fn tableBufferAlignment(self: *const Self) std.mem.Alignment {
            return std.mem.Alignment.fromByteUnits(@max(self.alignment, 16));
        }

        inline fn columnTickBase(meta: *const ColMeta, capacity: usize) usize {
            return std.mem.alignForward(usize, meta.offset + meta.size * capacity, @alignOf(TickInfo));
        }

        fn measureTableSize(self: *const Self, flags: *HeapFlagSet(FlagInt), capacity: usize) usize {
            const base_alignment = self.effectiveBaseAlignment();
            var running_offset = std.mem.alignForward(usize, @sizeOf(Entity) * capacity, base_alignment);
            for (self.columns.items) |meta| {
                const component_info = flags.getId(meta.flag);
                running_offset = std.mem.alignForward(usize, running_offset, component_info.alignment);
                running_offset += meta.size * capacity;
                running_offset = std.mem.alignForward(usize, running_offset, @alignOf(TickInfo));
                running_offset += @sizeOf(TickInfo) * capacity;
            }
            return running_offset;
        }

        fn assignTableOffsets(self: *Self, flags: *HeapFlagSet(FlagInt), capacity: usize) usize {
            const base_alignment = self.effectiveBaseAlignment();
            var running_offset = std.mem.alignForward(usize, @sizeOf(Entity) * capacity, base_alignment);
            for (self.columns.items) |*meta| {
                const component_info = flags.getId(meta.flag);
                running_offset = std.mem.alignForward(usize, running_offset, component_info.alignment);
                meta.offset = running_offset;
                running_offset += meta.size * capacity;
                running_offset = std.mem.alignForward(usize, running_offset, @alignOf(TickInfo));
                running_offset += @sizeOf(TickInfo) * capacity;
            }
            return running_offset;
        }

        pub fn addComp(self: *Self, gpa: std.mem.Allocator, flags: *HeapFlagSet(FlagInt), flag: CompFlag) !void {
            assert(self.len == 0);
            assert(self.capacity == 0);
            if (self.mask.contains(flag)) return;

            const id = flags.getId(flag);

            const col_meta = ColMeta{
                .size = id.size,
                .flag = flag,
                .offset = 0,
                .hash = id.hash,
            };

            const typeId = flags.getId(col_meta.flag);

            if (typeId.alignment > self.alignment) self.alignment = typeId.alignment;
            if (self.alignment == 0) self.alignment = 1;

            self.mask.insert(flag);
            try self.columns.append(gpa, col_meta);
            _ = self.assignTableOffsets(flags, self.capacity);
        }

        pub fn removeComp(self: *Self, flags: *HeapFlagSet(FlagInt), flag: CompFlag) void {
            assert(self.len == 0);
            assert(self.capacity == 0);
            self.mask.remove(flag);

            var removed_index: usize = 0;
            var found_removed_column = false;
            for (self.columns.items, 0..) |column_meta, column_index| {
                if (column_meta.flag == flag) {
                    removed_index = column_index;
                    found_removed_column = true;
                    break;
                }
            }
            assert(found_removed_column);

            _ = self.columns.swapRemove(removed_index);
            _ = self.assignTableOffsets(flags, self.capacity);
        }

        pub fn remove(self: *Self, gpa: std.mem.Allocator, entity: Entity) !void {
            const removed_index = self.entity_lookup.fetchRemove(entity) orelse return EcsError.EntityNotFound;
            assert(removed_index.value < self.len);
            self.destroyOwnedChildrenAtRow(gpa, removed_index.value);
            try self.swapRemoveRowPreservingOwned(gpa, removed_index.value);
        }

        fn destroyOwnedChildrenAtRow(self: *Self, gpa: std.mem.Allocator, row_index: usize) void {
            const children_meta = self.getMetaByHash(hashType(Children)) orelse return;
            const component_offset = children_meta.offset + children_meta.size * row_index;
            const children_ptr: *Children = @ptrCast(@alignCast(self.bytes[component_offset .. component_offset + children_meta.size]));
            children_ptr.items.deinit(gpa);
        }

        fn swapRemoveRowPreservingOwned(self: *Self, gpa: std.mem.Allocator, removed_index: usize) !void {
            assert(self.len > 0);
            assert(removed_index < self.len);
            defer self.len -= 1;
            if (removed_index == self.len - 1) return;
            const moved_entity = self.getEntity(self.len - 1);
            self.swapRemoveEntity(removed_index);
            for (self.columns.items) |*meta| {
                self.swapRemoveComp(removed_index, meta);
            }
            self.neutralizeStaleOwnedChildrenAtRow(self.len - 1);
            try self.entity_lookup.put(gpa, moved_entity, removed_index);
        }

        fn neutralizeStaleOwnedChildrenAtRow(self: *Self, stale_row_index: usize) void {
            const children_meta = self.getMetaByHash(hashType(Children)) orelse return;
            const component_offset = children_meta.offset + children_meta.size * stale_row_index;
            const children_ptr: *Children = @ptrCast(@alignCast(self.bytes[component_offset .. component_offset + children_meta.size]));
            children_ptr.* = .{};
        }

        pub fn put(self: *Self, gpa: std.mem.Allocator, flags: *HeapFlagSet(FlagInt), tick: u32, entity: Entity, bundle: anytype) !void {
            var row_is_new = false;
            var wrote_owned_children_for_rollback = false;
            const index: usize = blk: {
                const res = try self.entity_lookup.getOrPut(gpa, entity);
                if (res.found_existing) break :blk res.value_ptr.*;
                errdefer _ = self.entity_lookup.remove(entity);
                if (self.len >= self.capacity) {
                    try self.setCapacity(gpa, flags, @max(self.chunk_size, self.capacity * 2));
                }
                res.value_ptr.* = self.len;
                const offset = @sizeOf(Entity) * self.len;
                @memcpy(self.bytes[offset .. offset + @sizeOf(Entity)], std.mem.asBytes(&entity));
                row_is_new = true;
                self.len += 1;
                break :blk res.value_ptr.*;
            };
            errdefer if (row_is_new) {
                if (wrote_owned_children_for_rollback) {
                    self.destroyOwnedChildrenAtRow(gpa, index);
                }
                self.len -= 1;
                _ = self.entity_lookup.remove(entity);
            };

            assert(self.capacity > index);

            inline for (bundle) |comp| {
                if (isTuple(@TypeOf(comp))) continue;
                const flag = flags.getFlag(@TypeOf(comp));
                const meta = self.getMeta(flag).?;
                if (!row_is_new and @TypeOf(comp) == Children) {
                    const old_ptr: *Children = @ptrCast(@alignCast(self.getSingleRaw(index, meta)));
                    old_ptr.items.deinit(gpa);
                }
                self.putRaw(tick, tick, index, meta, std.mem.asBytes(&comp));
                if (@TypeOf(comp) == Children) {
                    wrote_owned_children_for_rollback = true;
                }
            }
        }

        pub inline fn putRaw(self: *Self, added_tick: u32, changed_tick: u32, index: usize, meta: *const ColMeta, bytes: []const u8) void {
            assert(index < self.capacity);
            const comp_offset = meta.offset + meta.size * index;
            @memcpy(self.bytes[comp_offset .. comp_offset + meta.size], bytes);
            const aligned_tick_base = Self.columnTickBase(meta, self.capacity);
            const tick_offset = aligned_tick_base + @sizeOf(TickInfo) * index;
            @memcpy(self.bytes[tick_offset .. tick_offset + @sizeOf(TickInfo)], std.mem.asBytes(&TickInfo{
                .added = added_tick,
                .changed = changed_tick,
            }));
        }

        pub inline fn putSingle(self: *Self, gpa: std.mem.Allocator, flags: *HeapFlagSet(FlagInt), tick: u32, entity: Entity, comp: anytype) EcsError!void {
            const index = self.entity_lookup.get(entity) orelse blk: {
                if (self.len >= self.capacity) {
                    try self.setCapacity(gpa, flags, @max(self.chunk_size, self.capacity * 2));
                }
                break :blk try self.putEntity(gpa, entity);
            };
            assert(self.capacity >= index);

            const flag = flags.getFlag(@TypeOf(comp));
            const meta = self.getMeta(flag).?;
            // if (already_present and @TypeOf(comp) == Children) {
            //     const old_ptr: *Children = @ptrCast(@alignCast(self.getSingleRaw(index, meta)));
            //     old_ptr.items.deinit(gpa);
            // }
            const bytes: []const u8 = @alignCast(std.mem.asBytes(&comp));
            self.putRaw(tick, tick, index, meta, bytes);
        }

        pub inline fn putEntity(self: *Self, gpa: std.mem.Allocator, entity: Entity) EcsError!usize {
            const res = try self.entity_lookup.getOrPut(gpa, entity);
            if (res.found_existing) return res.value_ptr.*;
            res.value_ptr.* = self.len;
            const offset = @sizeOf(Entity) * self.len;
            @memcpy(self.bytes[offset .. offset + @sizeOf(Entity)], std.mem.asBytes(&entity));
            self.len += 1;
            return res.value_ptr.*;
        }

        pub inline fn getEntity(self: *Self, index: usize) Entity {
            assert(self.len > index);
            const offset = @sizeOf(Entity) * index;
            const ent: *Entity = @ptrCast(@alignCast(self.bytes[offset .. offset + @sizeOf(Entity)]));
            return ent.*;
        }

        pub inline fn getSingleRawConst(self: *Self, index: usize, meta: *const ColMeta) []u8 {
            return self.getSingleRaw(index, meta);
        }

        pub inline fn getSingleRaw(self: *Self, index: usize, meta: *const ColMeta) []u8 {
            const comp_offset = meta.offset + meta.size * index;
            return self.bytes[comp_offset .. comp_offset + meta.size];
        }

        pub inline fn getSingle(self: *Self, flags: *HeapFlagSet(FlagInt), entity: Entity, comptime C: type) EcsError!*C {
            const index = self.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;
            const flag = flags.getFlag(C);
            const meta = self.getMeta(flag) orelse return EcsError.ComponentNotFound;
            return @ptrCast(@alignCast(self.getSingleRaw(index, meta)));
        }

        pub inline fn getSingleAndUpdate(self: *Self, flags: *HeapFlagSet(FlagInt), tick: u32, entity: Entity, comptime C: type) EcsError!*C {
            const index = self.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;
            const flag = flags.getFlag(C);
            const meta = self.getMeta(flag) orelse return EcsError.ComponentNotFound;
            const aligned_tick_base = Self.columnTickBase(meta, self.capacity);
            const tick_offset = aligned_tick_base + @sizeOf(TickInfo) * index;
            @memcpy(self.bytes[tick_offset + 4 .. tick_offset + 8], std.mem.asBytes(&tick));
            return @ptrCast(@alignCast(self.getSingleRaw(index, meta)));
        }

        pub inline fn upateChanged(self: *Self, flags: *HeapFlagSet(FlagInt), tick: u32, index: usize, comptime C: type) void {
            const flag = flags.getFlag(C);
            const meta = self.getMeta(flag).?;
            self.markChanged(tick, index, meta);
        }

        inline fn markChanged(self: *Self, tick: u32, index: usize, meta: *const ColMeta) void {
            const aligned_tick_base = Self.columnTickBase(meta, self.capacity);
            const tick_offset = aligned_tick_base + @sizeOf(TickInfo) * index;
            @memcpy(self.bytes[tick_offset + 4 .. tick_offset + 8], std.mem.asBytes(&tick));
        }

        pub inline fn getSingleConst(self: *Self, flags: *HeapFlagSet(FlagInt), entity: Entity, comptime C: type) EcsError!*const C {
            const index = self.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;
            const flag = flags.getFlag(C);
            const meta = self.getMeta(flag).?;
            return @ptrCast(@alignCast(self.getSingleRawConst(index, meta)));
        }

        pub inline fn getTickInfo(self: *Self, index: usize, meta: *const ColMeta) *const TickInfo {
            assert(self.len > index);
            const aligned_tick_base = Self.columnTickBase(meta, self.capacity);
            const tick_offset = aligned_tick_base + @sizeOf(TickInfo) * index;
            return @ptrCast(@alignCast(self.bytes[tick_offset .. tick_offset + @sizeOf(TickInfo)]));
        }

        pub inline fn getMeta(self: *Self, flag: CompFlag) ?*const ColMeta {
            for (self.columns.items) |*col| if (col.flag == flag) return col;
            return null;
        }

        pub inline fn getMetaByHash(self: *const Self, hash: u32) ?*const ColMeta {
            for (self.columns.items) |*col| if (col.hash == hash) return col;
            return null;
        }

        pub fn moveTo(
            self: *Self,
            gpa: std.mem.Allocator,
            flags: *HeapFlagSet(FlagInt),
            entity: Entity,
            dst: *Self,
        ) !void {
            assert(@intFromPtr(self) != @intFromPtr(dst));
            assert(dst.capacity >= dst.len);
            const intersection = self.mask.intersectWith(dst.mask);
            assert(intersection.eql(self.mask) or intersection.eql(dst.mask));
            const src_index = self.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;
            if (dst.len >= dst.capacity) {
                try dst.setCapacity(gpa, flags, @max(dst.chunk_size, dst.capacity * 2));
            }
            const dst_index = try dst.putEntity(gpa, entity);
            errdefer _ = dst.entity_lookup.remove(entity);
            errdefer dst.len -= 1;
            var skipped_count: u32 = 0;
            var skipped_is_owned_children = false;
            for (self.columns.items) |*meta| {
                const data = self.getSingleRawConst(src_index, meta);
                const info = self.getTickInfo(src_index, meta);
                const dst_meta = dst.getMeta(meta.flag) orelse {
                    skipped_count += 1;
                    if (meta.hash == hashType(Children)) skipped_is_owned_children = true;
                    continue;
                };
                dst.putRaw(info.added, info.changed, dst_index, dst_meta, data);
            }
            assert(skipped_count < 2);
            if (skipped_is_owned_children) {
                self.destroyOwnedChildrenAtRow(gpa, src_index);
            }
            _ = self.entity_lookup.remove(entity);
            try self.swapRemoveRowPreservingOwned(gpa, src_index);
        }

        inline fn swapRemoveComp(self: *Self, index: usize, meta: *ColMeta) void {
            assert(self.len > 0);
            const remove_offset = meta.offset + meta.size * index;
            const last_offset = meta.offset + meta.size * (self.len - 1);
            const aligned_tick_base = Self.columnTickBase(meta, self.capacity);
            const tick_remove_offset = aligned_tick_base + @sizeOf(TickInfo) * index;
            const tick_last_offset = aligned_tick_base + @sizeOf(TickInfo) * (self.len - 1);

            @memcpy(
                self.bytes[remove_offset .. remove_offset + meta.size],
                self.bytes[last_offset .. last_offset + meta.size],
            );

            @memcpy(
                self.bytes[tick_remove_offset .. tick_remove_offset + @sizeOf(TickInfo)],
                self.bytes[tick_last_offset .. tick_last_offset + @sizeOf(TickInfo)],
            );
        }

        inline fn swapRemoveEntity(self: *Self, index: usize) void {
            assert(self.len > 0);

            const ent_size = @sizeOf(Entity);
            const remove_offset = ent_size * index;
            const last_offset = ent_size * (self.len - 1);

            @memcpy(
                self.bytes[remove_offset .. remove_offset + ent_size],
                self.bytes[last_offset .. last_offset + ent_size],
            );
        }

        pub fn cloneEmpty(self: *const Self, gpa: std.mem.Allocator) !Self {
            var clone = Self{};
            clone.bytes = &.{};
            clone.mask = self.mask;
            clone.alignment = self.alignment;
            clone.columns = try self.columns.clone(gpa);
            clone.chunk_size = self.chunk_size;
            return clone;
        }

        /// Query struct by index
        pub fn getQueryIndex(self: *Self, flags: *HeapFlagSet(FlagInt), index: usize, comptime Q: type) EcsError!Q {
            var row: Q = undefined;
            const info = @typeInfo(Q).@"struct";

            inline for (info.field_names, info.field_types) |f_name, f_type| {
                // entity
                if (f_type == Entity) {
                    @field(row, f_name) = self.getEntity(index);
                    continue;
                }
                // meta
                if (f_type == Meta) {
                    @field(row, f_name) = .{
                        ._columns = self.columns.items,
                        ._mask = &self.mask,
                    };
                    continue;
                }
                // skip tags
                if (@sizeOf(f_type) == 0) {
                    @field(row, f_name) = .{};
                    continue;
                }

                if (@typeInfo(f_type) == .@"struct") {
                    if (@hasDecl(f_type, "_is_has")) {
                        const flag = flags.getFlag(f_type.inner);
                        const has = self.mask.contains(flag);
                        @field(row, f_name) = .{ .val = has };
                        continue;
                    }
                }

                const field_ptr = switch (@typeInfo(f_type)) {
                    .pointer => @typeInfo(f_type).pointer,
                    .optional => |opt| @typeInfo(opt.child).pointer,
                    else => @compileError("Type not allowed: " ++ f_name),
                };

                const hash = comptime hashType(field_ptr.child);
                if (self.getMetaByHash(hash)) |meta| {
                    if (field_ptr.attrs.@"const") {
                        const raw_bytes = self.getSingleRawConst(index, meta);
                        @field(row, f_name) = @ptrCast(@alignCast(raw_bytes));
                    } else {
                        const raw_bytes = self.getSingleRaw(index, meta);
                        @field(row, f_name) = @ptrCast(@alignCast(raw_bytes));
                    }
                } else {
                    if (@typeInfo(f_type) == .optional) {
                        @field(row, f_name) = null;
                    } else {
                        return EcsError.ComponentNotFound;
                    }
                }
            }

            return row;
        }

        /// Query struct by index using pre-resolved meta pointers (avoids per-entity lookups)
        pub inline fn getQueryIndexCached(self: *Self, index: usize, comptime Q: type, cached_metas: *const [queryMetaCount(Q)]?*const ColMeta) Q {
            var row: Q = undefined;
            const info = @typeInfo(Q).@"struct";
            comptime var meta_idx: usize = 0;

            inline for (info.field_names, info.field_types) |f_name, f_type| {
                if (f_type == Entity) {
                    @field(row, f_name) = self.getEntity(index);
                    continue;
                }
                if (f_type == Meta) {
                    @field(row, f_name) = .{
                        ._columns = self.columns.items,
                        ._mask = &self.mask,
                    };
                    continue;
                }
                if (@sizeOf(f_type) == 0) {
                    @field(row, f_name) = .{};
                    continue;
                }
                if (@typeInfo(f_type) == .@"struct") {
                    if (@hasDecl(f_type, "_is_has")) {
                        @field(row, f_name) = .{ .val = cached_metas[meta_idx] != null };
                        meta_idx += 1;
                        continue;
                    }
                }

                const field_ptr = switch (@typeInfo(f_type)) {
                    .pointer => @typeInfo(f_type).pointer,
                    .optional => |opt| @typeInfo(opt.child).pointer,
                    else => @compileError("Type not allowed: " ++ f_name),
                };

                if (cached_metas[meta_idx]) |meta| {
                    if (field_ptr.attrs.@"const") {
                        @field(row, f_name) = @ptrCast(@alignCast(self.getSingleRawConst(index, meta)));
                    } else {
                        @field(row, f_name) = @ptrCast(@alignCast(self.getSingleRaw(index, meta)));
                    }
                } else {
                    if (@typeInfo(f_type) == .optional) {
                        @field(row, f_name) = null;
                    } else {
                        unreachable;
                    }
                }
                meta_idx += 1;
            }

            return row;
        }

        /// Resolve meta pointers for a query type against this archetype
        pub inline fn resolveQueryMetas(self: *Self, flags: *HeapFlagSet(FlagInt), comptime Q: type) [queryMetaCount(Q)]?*const ColMeta {
            const info = @typeInfo(Q).@"struct";
            var metas: [queryMetaCount(Q)]?*const ColMeta = undefined;
            comptime var meta_idx: usize = 0;

            inline for (info.field_names, info.field_types) |f_name, f_type| {
                if (f_type == Entity or f_type == Meta or @sizeOf(f_type) == 0) continue;

                if (@typeInfo(f_type) == .@"struct") {
                    if (@hasDecl(f_type, "_is_has")) {
                        const flag = flags.getFlag(f_type.inner);
                        metas[meta_idx] = if (self.mask.contains(flag)) self.getMeta(flag) else null;
                        meta_idx += 1;
                        continue;
                    }
                }

                const field_ptr = switch (@typeInfo(f_type)) {
                    .pointer => @typeInfo(f_type).pointer,
                    .optional => |opt| @typeInfo(opt.child).pointer,
                    else => @compileError("Type not allowed: " ++ f_name),
                };
                const hash = comptime hashType(field_ptr.child);
                metas[meta_idx] = self.getMetaByHash(hash);
                meta_idx += 1;
            }

            return metas;
        }

        inline fn calcTableOffsets(self: *Self, flags: *HeapFlagSet(FlagInt)) void {
            assert(self.len == 0);
            _ = self.assignTableOffsets(flags, self.capacity);
        }

        pub fn setCapacity(self: *Self, gpa: std.mem.Allocator, flags: *HeapFlagSet(FlagInt), new_capacity: usize) !void {
            assert(new_capacity >= self.len);
            const lifetime_table_buffer_alignment = self.tableBufferAlignment();
            const required_table_size = self.measureTableSize(flags, new_capacity);
            const freshly_allocated_table_bytes = (gpa.rawAlloc(
                required_table_size,
                lifetime_table_buffer_alignment,
                @returnAddress(),
            ) orelse return error.OutOfMemory)[0..required_table_size];
            if (self.capacity == 0) {
                self.bytes = freshly_allocated_table_bytes;
                self.capacity = new_capacity;
                self.allocated_table_alignment = lifetime_table_buffer_alignment;
                _ = self.assignTableOffsets(flags, self.capacity);
                return;
            }
            const entity_size = @sizeOf(Entity) * self.len;
            @memcpy(freshly_allocated_table_bytes[0..entity_size], self.bytes[0..entity_size]);
            const base_alignment = self.effectiveBaseAlignment();
            var running_offset = std.mem.alignForward(usize, @sizeOf(Entity) * new_capacity, base_alignment);
            for (self.columns.items) |*meta| {
                const component_info = flags.getId(meta.flag);
                running_offset = std.mem.alignForward(usize, running_offset, component_info.alignment);
                const old_component_offset = meta.offset;
                const old_tick_base = Self.columnTickBase(meta, self.capacity);
                const copy_size = meta.size * self.len;
                @memcpy(
                    freshly_allocated_table_bytes[running_offset .. running_offset + copy_size],
                    self.bytes[old_component_offset .. old_component_offset + copy_size],
                );
                meta.offset = running_offset;
                running_offset += meta.size * new_capacity;
                running_offset = std.mem.alignForward(usize, running_offset, @alignOf(TickInfo));
                const tick_size = @sizeOf(TickInfo) * self.len;
                @memcpy(
                    freshly_allocated_table_bytes[running_offset .. running_offset + tick_size],
                    self.bytes[old_tick_base .. old_tick_base + tick_size],
                );
                running_offset += @sizeOf(TickInfo) * new_capacity;
            }
            gpa.rawFree(self.bytes, self.allocated_table_alignment, @returnAddress());
            self.bytes = freshly_allocated_table_bytes;
            self.capacity = new_capacity;
            self.allocated_table_alignment = lifetime_table_buffer_alignment;
        }

        pub fn releaseAllArchTableMemory(self: *Self, gpa: std.mem.Allocator) void {
            for (self.columns.items) |*column_meta| {
                if (column_meta.hash == hashType(Children)) {
                    var row_index: usize = 0;
                    while (row_index < self.len) : (row_index += 1) {
                        const component_offset = column_meta.offset + column_meta.size * row_index;
                        const children_ptr: *Children = @ptrCast(@alignCast(self.bytes[component_offset .. component_offset + column_meta.size]));
                        children_ptr.items.deinit(gpa);
                    }
                }
            }
            if (self.capacity > 0) {
                gpa.rawFree(self.bytes, self.allocated_table_alignment, @returnAddress());
            }
            self.bytes = &.{};
            self.columns.deinit(gpa);
            self.entity_lookup.deinit(gpa);
            self.len = 0;
            self.capacity = 0;
        }
    };
}

// *************************************************
// Arch Registry
// *************************************************
fn ComponentRegistry(FlagInt: type) type {
    return struct {
        component_flags: HeapFlagSet(FlagInt) = .{},
        /// entity -> arch id
        entity_lookup: std.AutoHashMapUnmanaged(Entity, usize) = .empty,
        /// group mask -> arch id
        archtypes_lookup: std.AutoHashMapUnmanaged(HeapFlagSet(FlagInt).Set, usize) = .empty,
        archtypes: std.ArrayList(ArchType(FlagInt)) = .empty,

        /// comp hash -> codec
        codecs: std.AutoHashMapUnmanaged(u32, ComponentCodec) = .empty,

        const FlagSet = HeapFlagSet(FlagInt);
        const CompFlag = FlagSet.Flag;
        pub const ComponentCodec = struct {
            name: []const u8,
            serialize: *const fn (*const anyopaque, *std.Io.Writer) anyerror!void,
            deserialize: *const fn (*anyopaque, std.mem.Allocator, *std.Io.Reader) anyerror!void,
        };

        const Self = @This();
        const CHUNK_SIZE: usize = 64;

        pub fn registerCodec(self: *Self, allocator: std.mem.Allocator, comptime C: type, codec: ComponentCodec) !void {
            _ = self.component_flags.getFlag(C);
            try self.codecs.put(allocator, hashType(C), codec);
        }

        pub fn add(self: *Self, allocator: std.mem.Allocator, tick: u32, entity: Entity, comp: anytype) !void {
            const CompType = @TypeOf(comp);
            const flag = self.component_flags.getFlag(CompType);

            const current_arch_id = self.entity_lookup.get(entity);
            var mask = if (current_arch_id) |aid| self.archtypes.items[aid].mask else HeapFlagSet(FlagInt).Set.empty;

            if (mask.contains(flag)) {
                const arch = &self.archtypes.items[current_arch_id.?];
                try arch.put(allocator, &self.component_flags, tick, entity, .{comp});
                return;
            }

            mask.insert(flag);

            // add to new
            const new_arch_id = try self.archtypes_lookup.getOrPut(allocator, mask);
            if (!new_arch_id.found_existing) {
                // create
                if (current_arch_id) |current_id| {
                    const cloned = try self.archtypes.items[current_id].cloneEmpty(allocator);
                    try self.archtypes.append(allocator, cloned);
                    new_arch_id.value_ptr.* = self.archtypes.items.len - 1;
                } else {
                    try self.archtypes.append(allocator, .{ .chunk_size = CHUNK_SIZE });
                    new_arch_id.value_ptr.* = self.archtypes.items.len - 1;
                }

                // add new comp
                try self.archtypes.items[new_arch_id.value_ptr.*].addComp(allocator, &self.component_flags, flag);
                try self.archtypes.items[new_arch_id.value_ptr.*].setCapacity(allocator, &self.component_flags, CHUNK_SIZE);
            }

            if (current_arch_id) |current_id| {
                try self.archtypes.items[current_id].moveTo(
                    allocator,
                    &self.component_flags,
                    entity,
                    &self.archtypes.items[new_arch_id.value_ptr.*],
                );
                try self.archtypes.items[new_arch_id.value_ptr.*].putSingle(allocator, &self.component_flags, tick, entity, comp);
            } else {
                try self.archtypes.items[new_arch_id.value_ptr.*].put(allocator, &self.component_flags, tick, entity, .{comp});
            }

            try self.entity_lookup.put(allocator, entity, new_arch_id.value_ptr.*);
        }

        /// Insert raw component bytes into an entity by flag. Used for deserialization.
        pub fn addRaw(self: *Self, allocator: std.mem.Allocator, tick: u32, entity: Entity, flag: CompFlag, bytes: []const u8) !void {
            const current_arch_id = self.entity_lookup.get(entity);
            var mask = if (current_arch_id) |aid| self.archtypes.items[aid].mask else HeapFlagSet(FlagInt).Set.empty;

            if (mask.contains(flag)) {
                const arch = &self.archtypes.items[current_arch_id.?];
                const meta = arch.getMeta(flag).?;
                const index = arch.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;
                if (meta.hash == hashType(Children)) {
                    const old_children: *Children = @ptrCast(@alignCast(arch.getSingleRaw(index, meta)));
                    old_children.items.deinit(allocator);
                }
                arch.putRaw(tick, tick, index, meta, bytes);
                return;
            }

            mask.insert(flag);

            const new_arch_id = try self.archtypes_lookup.getOrPut(allocator, mask);
            if (!new_arch_id.found_existing) {
                if (current_arch_id) |current_id| {
                    const cloned = try self.archtypes.items[current_id].cloneEmpty(allocator);
                    try self.archtypes.append(allocator, cloned);
                    new_arch_id.value_ptr.* = self.archtypes.items.len - 1;
                } else {
                    try self.archtypes.append(allocator, .{ .chunk_size = CHUNK_SIZE });
                    new_arch_id.value_ptr.* = self.archtypes.items.len - 1;
                }

                try self.archtypes.items[new_arch_id.value_ptr.*].addComp(allocator, &self.component_flags, flag);
                try self.archtypes.items[new_arch_id.value_ptr.*].setCapacity(allocator, &self.component_flags, CHUNK_SIZE);
            }

            if (current_arch_id) |current_id| {
                try self.archtypes.items[current_id].moveTo(
                    allocator,
                    &self.component_flags,
                    entity,
                    &self.archtypes.items[new_arch_id.value_ptr.*],
                );
            } else {
                const arch = &self.archtypes.items[new_arch_id.value_ptr.*];
                if (arch.len >= arch.capacity) {
                    try arch.setCapacity(allocator, &self.component_flags, @max(arch.chunk_size, arch.capacity * 2));
                }
                _ = try arch.putEntity(allocator, entity);
            }

            // write raw bytes
            const arch = &self.archtypes.items[new_arch_id.value_ptr.*];
            const meta = arch.getMeta(flag).?;
            const index = arch.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;
            arch.putRaw(tick, tick, index, meta, bytes);

            try self.entity_lookup.put(allocator, entity, new_arch_id.value_ptr.*);
        }

        pub fn addBundle(self: *Self, allocator: std.mem.Allocator, tick: u32, entity: Entity, bundle: anytype) !void {
            const current_arch_id = self.entity_lookup.get(entity);
            const is_new_entity = current_arch_id == null;

            var mask = if (current_arch_id) |aid| self.archtypes.items[aid].mask else HeapFlagSet(FlagInt).Set.empty;
            const old_mask = mask;
            inline for (bundle) |comp| {
                const CompType = @TypeOf(comp);
                if (isTuple(CompType)) continue;

                const flag = self.component_flags.getFlag(CompType);
                mask.insert(flag);
            }

            const same_arch = mask.eql(old_mask);
            if (!is_new_entity and same_arch) {
                return self.archtypes.items[current_arch_id.?].put(allocator, &self.component_flags, tick, entity, bundle);
            }

            const next_arch_id = try self.archtypes_lookup.getOrPut(allocator, mask);
            if (!next_arch_id.found_existing) {
                if (is_new_entity) {
                    try self.archtypes.append(allocator, .{ .chunk_size = CHUNK_SIZE });
                } else {
                    const cloned = try self.archtypes.items[current_arch_id.?].cloneEmpty(allocator);
                    try self.archtypes.append(allocator, cloned);
                }

                next_arch_id.value_ptr.* = self.archtypes.items.len - 1;

                inline for (bundle) |comp| {
                    const CompType = @TypeOf(comp);
                    if (isTuple(CompType)) continue;

                    const flag = self.component_flags.getFlag(CompType);
                    try self.archtypes.items[next_arch_id.value_ptr.*].addComp(allocator, &self.component_flags, flag);
                }

                try self.archtypes.items[next_arch_id.value_ptr.*].setCapacity(allocator, &self.component_flags, CHUNK_SIZE);
            }

            if (!is_new_entity) {
                try self.archtypes.items[current_arch_id.?].moveTo(
                    allocator,
                    &self.component_flags,
                    entity,
                    &self.archtypes.items[next_arch_id.value_ptr.*],
                );
            }

            try self.archtypes.items[next_arch_id.value_ptr.*].put(allocator, &self.component_flags, tick, entity, bundle);
            _ = try self.entity_lookup.put(allocator, entity, next_arch_id.value_ptr.*);
        }

        pub fn getSingle(self: *Self, entity: Entity, comptime C: type) ?*C {
            const arch_id = self.entity_lookup.get(entity) orelse return null;
            return self.archtypes.items[arch_id].getSingle(&self.component_flags, entity, C) catch null;
        }

        pub fn getSingleAndUpdate(self: *Self, tick: u32, entity: Entity, comptime C: type) ?*C {
            const arch_id = self.entity_lookup.get(entity) orelse return null;
            return self.archtypes.items[arch_id].getSingleAndUpdate(&self.component_flags, tick, entity, C) catch null;
        }

        pub fn getSingleOpaque(self: *Self, entity: Entity, flag: CompFlag) ?*anyopaque {
            const arch_id = self.entity_lookup.get(entity) orelse return null;
            const meta = self.archtypes.items[arch_id].getMeta(flag) orelse return null;
            const index = self.archtypes.items[arch_id].entity_lookup.get(entity) orelse return null;
            return self.archtypes.items[arch_id].getSingleRaw(index, meta).ptr;
        }

        /// Raw pointer to a live component, with the changed tick bumped so
        /// change-detection systems pick up the mutation. Used by the debug
        /// server to apply JSON patches in place.
        pub fn getSingleOpaqueAndUpdate(self: *Self, tick: u32, entity: Entity, flag: CompFlag) ?*anyopaque {
            const arch_id = self.entity_lookup.get(entity) orelse return null;
            const arch = &self.archtypes.items[arch_id];
            const meta = arch.getMeta(flag) orelse return null;
            const index = arch.entity_lookup.get(entity) orelse return null;
            const aligned_tick_base = ArchType(FlagInt).columnTickBase(meta, arch.capacity);
            const tick_offset = aligned_tick_base + @sizeOf(ArchType(FlagInt).TickInfo) * index;
            @memcpy(arch.bytes[tick_offset + 4 .. tick_offset + 8], std.mem.asBytes(&tick));
            return arch.getSingleRaw(index, meta).ptr;
        }

        /// Remove one component from an entity by runtime flag.
        /// No-op when the entity does not carry the component.
        pub fn removeByFlag(self: *Self, allocator: std.mem.Allocator, entity: Entity, flag: CompFlag) !void {
            const current_arch_id = self.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;

            const current_arch = &self.archtypes.items[current_arch_id];
            if (current_arch.getMeta(flag) == null) return;

            const mask = current_arch.mask;
            if (!mask.contains(flag)) return;

            var new_mask = mask;
            new_mask.remove(flag);

            const new_arch_id = try self.archtypes_lookup.getOrPut(allocator, new_mask);

            if (!new_arch_id.found_existing) {
                var new_arch = try self.archtypes.items[current_arch_id].cloneEmpty(allocator);
                new_arch.removeComp(&self.component_flags, flag);
                try new_arch.setCapacity(allocator, &self.component_flags, CHUNK_SIZE);
                try self.archtypes.append(allocator, new_arch);
                new_arch_id.value_ptr.* = self.archtypes.items.len - 1;
            }

            const new_arch = &self.archtypes.items[new_arch_id.value_ptr.*];
            try self.archtypes.items[current_arch_id].moveTo(allocator, &self.component_flags, entity, new_arch);

            _ = try self.entity_lookup.put(allocator, entity, new_arch_id.value_ptr.*);
        }

        pub fn remove(self: *Self, allocator: std.mem.Allocator, entity: Entity, comptime C: type) !void {
            const current_arch_id = self.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;

            // linear lookup in current arch is faster then the registry
            const meta = self.archtypes.items[current_arch_id].getMetaByHash(hashType(C)) orelse {
                return;
            };

            return self.removeByFlag(allocator, entity, meta.flag);
        }

        pub fn despawn(self: *Self, allocator: std.mem.Allocator, entity: Entity) !void {
            const arch_id = self.entity_lookup.get(entity) orelse return;
            try self.archtypes.items[arch_id].remove(allocator, entity);
            _ = self.entity_lookup.remove(entity);
        }

        pub fn releaseAllComponentRegistryMemory(self: *Self, gpa: std.mem.Allocator) void {
            for (self.archtypes.items) |*archTable| {
                archTable.releaseAllArchTableMemory(gpa);
            }
            self.archtypes.deinit(gpa);
            self.entity_lookup.deinit(gpa);
            self.archtypes_lookup.deinit(gpa);
            self.codecs.deinit(gpa);
        }
    };
}

fn ArchScope(FlagInt: type) type {
    return struct {
        arch: *ArchType(FlagInt),
        set_id: u32,
    };
}

fn ArchSopeIter(FlagInt: type, comptime arch_only: bool) type {
    return struct {
        const Self = @This();
        const empty = HeapFlagSet(FlagInt).Set.empty;

        const Result = if (arch_only) *ArchType(FlagInt) else ArchScope(FlagInt);

        reg: []ArchType(FlagInt),
        matched_archtypes: []ArchEntry,
        i: u32 = 0,

        pub fn next(self: *Self) ?Result {
            if (self.i >= self.matched_archtypes.len) return null;
            const next_arch = self.matched_archtypes[self.i];

            self.i += 1;
            if (arch_only) {
                return &self.reg[next_arch.arch_id];
            } else {
                return .{
                    .arch = &self.reg[next_arch.arch_id],
                    .set_id = next_arch.set_id,
                };
            }
        }

        pub fn reset(self: *@This()) void {
            self.i = 0;
        }
    };
}

//---------------------------------------
pub fn QueryIter(comptime FlagInt: type, comptime Q: type, comptime filter: *const Filter) type {
    const ArchOnly = filter.IsArchOnly();
    const branch_ticks = comptime filter.comptimeBranchTicks();
    const Arch = ArchType(FlagInt);
    const meta_count = Arch.queryMetaCount(Q);

    // Total tick meta count across all branches (for caching tick filter lookups)
    const tick_meta_count = comptime blk: {
        var count: usize = 0;
        for (branch_ticks) |branch| {
            count += branch.added.len + branch.changed.len;
        }
        break :blk count;
    };

    return struct {
        const Self = @This();
        const empty = HeapFlagSet(FlagInt).Set.empty;
        const ArchResult = if (ArchOnly) *Arch else ArchScope(FlagInt);

        flags: *HeapFlagSet(FlagInt),
        arch_iter: ArchSopeIter(FlagInt, ArchOnly),
        current_arch: ?ArchResult = null,
        offset: usize = 0,
        world_tick: u32,
        cached_metas: [meta_count]?*const Arch.ColMeta = undefined,
        cached_tick_metas: [tick_meta_count]?*const Arch.ColMeta = undefined,

        pub fn next(self: *Self) ?Q {
            while (true) {
                if (self.current_arch) |entry| {
                    const arch = if (ArchOnly) entry else entry.arch;
                    if (self.offset >= arch.len) {
                        self.current_arch = null;
                        self.offset = 0;
                        continue;
                    }

                    if (!ArchOnly) {
                        if (!passesTickFilterCached(entry.arch, entry.set_id, self.world_tick, self.offset, &self.cached_tick_metas)) {
                            self.offset += 1;
                            continue;
                        }
                    }

                    const next_item = arch.getQueryIndexCached(self.offset, Q, &self.cached_metas);
                    self.offset += 1;
                    return next_item;
                }

                const entry = self.arch_iter.next() orelse return null;
                self.current_arch = entry;
                self.offset = 0;

                // Resolve meta pointers once per archetype
                const arch = if (ArchOnly) entry else entry.arch;
                self.cached_metas = arch.resolveQueryMetas(self.flags, Q);

                if (!ArchOnly) {
                    self.resolveTickMetas(arch);
                }
            }
        }

        fn resolveTickMetas(self: *Self, arch: *Arch) void {
            comptime var idx: usize = 0;
            inline for (branch_ticks) |branch| {
                inline for (branch.added) |hash| {
                    self.cached_tick_metas[idx] = arch.getMetaByHash(hash);
                    idx += 1;
                }
                inline for (branch.changed) |hash| {
                    self.cached_tick_metas[idx] = arch.getMetaByHash(hash);
                    idx += 1;
                }
            }
        }

        // Comptime offsets into cached_tick_metas for each branch
        const branch_offsets = blk: {
            var offsets: [branch_ticks.len]usize = undefined;
            var off: usize = 0;
            for (branch_ticks, 0..) |branch, i| {
                offsets[i] = off;
                off += branch.added.len + branch.changed.len;
            }
            break :blk offsets;
        };

        inline fn passesTickFilterCached(arch: *Arch, set_id: u32, tick: u32, index: usize, tick_metas: *const [tick_meta_count]?*const Arch.ColMeta) bool {
            inline for (branch_ticks, 0..) |branch, i| {
                if (set_id == i) {
                    const base = branch_offsets[i];
                    inline for (0..branch.added.len) |j| {
                        const meta = tick_metas[base + j] orelse return true;
                        const info = arch.getTickInfo(index, meta);
                        if (info.added < tick) return false;
                    }
                    inline for (0..branch.changed.len) |j| {
                        const meta = tick_metas[base + branch.added.len + j] orelse return true;
                        const info = arch.getTickInfo(index, meta);
                        if (info.changed < tick -| 1) return false;
                    }
                    return true;
                }
            }
            return true;
        }

        pub fn reset(self: *Self) void {
            self.arch_iter.reset();
            self.current_arch = null;
            self.offset = 0;
        }

        /// mark a component of the current iteration as changed.
        /// should only be called inside a iteration loop.
        pub fn changed(self: *Self, comptime C: type) void {
            assert(self.offset > 0);
            assert(self.current_arch != null);
            const index = self.offset - 1; // current iteration
            const arch = if (ArchOnly) self.current_arch.? else self.current_arch.?.arch;
            comptime var meta_idx: usize = 0;
            inline for (@typeInfo(Q).@"struct".field_types) |f_type| {
                if (f_type == Entity or f_type == Arch.Meta or @sizeOf(f_type) == 0) continue;
                const component = switch (@typeInfo(f_type)) {
                    .pointer => |ptr| ptr.child,
                    .optional => |opt| @typeInfo(opt.child).pointer.child,
                    else => void,
                };
                if (component == C) {
                    arch.markChanged(self.world_tick, index, self.cached_metas[meta_idx].?);
                    return;
                }
                meta_idx += 1;
            }
            arch.upateChanged(self.flags, self.world_tick, index, C);
        }
    };
}

fn addFilterAccess(
    comptime FlagInt: type,
    comptime filter: *const Filter,
    flags: *HeapFlagSet(FlagInt),
    access: *Access(FlagInt),
) void {
    switch (filter.*) {
        .with, .without, .added, .changed => |hash| {
            if (flags.getFlagFromHash(hash)) |flag| {
                access.comp_read_write.insert(flag);
            }
        },
        .@"and", .@"or" => |children| {
            inline for (children) |child| {
                addFilterAccess(FlagInt, &child, flags, access);
            }
        },
        .empty => {},
    }
}

pub fn IQueryStructFilteredNew(comptime desc: AppDesc, comptime QueryStruct: type, comptime filter: Filter) type {
    return struct {
        const Self = @This();
        const FlagSet = HeapFlagSet(desc.FlagInt);

        state: *QueryState(desc.FlagInt, QueryStruct, filter),
        reg: *ComponentRegistry(desc.FlagInt),
        world_tick: u32,

        pub fn addAccess(world: *App(desc), access: *Access(desc.FlagInt)) void {
            const flags = &world.components.component_flags;
            const QueryInfo = @typeInfo(QueryStruct);
            inline for (QueryInfo.@"struct".field_types) |f_type| {
                switch (@typeInfo(f_type)) {
                    .optional => |opt| {
                        switch (@typeInfo(opt.child)) {
                            .pointer => |ptr| {
                                const comp_id = flags.getFlag(ptr.child);
                                access.comp_read_write.insert(comp_id);
                                if (!ptr.attrs.@"const") access.comp_write.insert(comp_id);
                            },
                            else => {},
                        }
                    },
                    .pointer => |ptr| {
                        const comp_id = flags.getFlag(ptr.child);
                        access.comp_read_write.insert(comp_id);
                        if (!ptr.attrs.@"const") access.comp_write.insert(comp_id);
                    },
                    else => {},
                }
            }
            addFilterAccess(desc.FlagInt, &filter, flags, access);
        }

        pub fn iter(self: *const Self) QueryIter(desc.FlagInt, QueryStruct, &filter) {
            return QueryIter(desc.FlagInt, QueryStruct, &filter){
                .flags = &self.reg.component_flags,
                .world_tick = self.world_tick,
                .arch_iter = ArchSopeIter(desc.FlagInt, filter.IsArchOnly()){
                    .matched_archtypes = self.state.matched_archtypes.items,
                    .reg = self.reg.archtypes.items,
                },
            };
        }

        pub fn first(self: *const Self) ?QueryStruct {
            var it = self.iter();
            return it.next();
        }

        pub fn contains(self: *const Self, entity: Entity) bool {
            const arch_id = self.reg.entity_lookup.get(entity) orelse return false;
            for (self.state.matched_archtypes.items) |*en| {
                if (en.arch_id == arch_id) return true;
            }
            return false;
        }

        pub fn get(self: *const Self, entity: Entity) EcsError!QueryStruct {
            const arch_id = self.reg.entity_lookup.get(entity) orelse return EcsError.EntityNotFound;

            var found = false;
            for (self.state.matched_archtypes.items) |*en| {
                if (en.arch_id == arch_id) {
                    found = true;
                    break;
                }
            }

            if (!found) return error.EntityNotFound;

            const arch = &self.reg.archtypes.items[arch_id];
            const index = arch.entity_lookup.get(entity).?;

            return arch.getQueryIndex(&self.reg.component_flags, index, QueryStruct);
        }

        /// totoal entity count for query
        pub fn count(self: *const Self) usize {
            var c: usize = 0;
            for (self.state.matched_archtypes.items) |entry| {
                c += self.reg.archtypes.items[entry.arch_id].len;
            }
            return c;
        }

        pub fn setWorldTick(self: *Self, tick: u32) void {
            self.world_tick = tick;
        }

        pub fn fromLocal(world: *App(desc), locals: *ResourceRegistry(desc.FlagInt)) EcsError!Self {
            const QS = QueryState(desc.FlagInt, QueryStruct, filter);
            const state: *QS = try locals.getOrDefault(world.memtator.world(), QS);

            if (state.created_on == null) {
                state.* = try QS.new(
                    world.memtator.world(),
                    &world.components.component_flags,
                    world.components.archtypes.items,
                    world.world_tick,
                );
            } else if (state.last_update < world.components.archtypes.items.len) {
                state.build_access_set(&world.components.component_flags);
                try state.build_match(world.memtator.world(), world.components.archtypes.items);
                state.last_update = @intCast(world.components.archtypes.items.len);
            }

            return Self{
                .reg = &world.components,
                .world_tick = world.world_tick,
                .state = state,
            };
        }

        pub fn fromWorld(world: *App(desc)) EcsError!Self {
            const QS = QueryState(desc.FlagInt, QueryStruct, filter);
            const frame_gpa = world.memtator.frame();
            const state: *QS = try frame_gpa.create(QS);

            state.* = try QS.new(
                frame_gpa,
                &world.components.component_flags,
                world.components.archtypes.items,
                world.world_tick,
            );

            return Self{
                .reg = &world.components,
                .world_tick = world.world_tick,
                .state = state,
            };
        }
    };
}

pub fn HookRegistry(desc: AppDesc) type {
    return struct {
        has_add_hook: FlagSet.Set = .{},
        has_remove_hook: FlagSet.Set = .{},
        has_despawn_hook: FlagSet.Set = .{},
        // --------------------
        add_hooks: std.AutoHashMapUnmanaged(FlagSet.Flag, std.ArrayList(Hook)) = .empty,
        remove_hooks: std.AutoHashMapUnmanaged(FlagSet.Flag, std.ArrayList(Hook)) = .empty,
        despawn_hooks: std.AutoHashMapUnmanaged(FlagSet.Flag, std.ArrayList(Hook)) = .empty,

        const Self = @This();
        const World = App(desc);
        const FlagSet = HeapFlagSet(desc.FlagInt);
        pub const HookFn = *const fn (*anyopaque, Entity, *World) EcsError!void;
        pub const Hook = struct { run: HookFn };

        pub fn releaseAllHookRegistryMemory(self: *Self, gpa: std.mem.Allocator) void {
            var addHookIterator = self.add_hooks.iterator();
            while (addHookIterator.next()) |hookListEntry| {
                hookListEntry.value_ptr.deinit(gpa);
            }
            self.add_hooks.deinit(gpa);
            var removeHookIterator = self.remove_hooks.iterator();
            while (removeHookIterator.next()) |hookListEntry| {
                hookListEntry.value_ptr.deinit(gpa);
            }
            self.remove_hooks.deinit(gpa);
            var despawnHookIterator = self.despawn_hooks.iterator();
            while (despawnHookIterator.next()) |hookListEntry| {
                hookListEntry.value_ptr.deinit(gpa);
            }
            self.despawn_hooks.deinit(gpa);
        }

        pub fn clear(self: *Self, gpa: std.mem.Allocator) void {
            self.releaseAllHookRegistryMemory(gpa);
            self.has_add_hook = .{};
            self.has_remove_hook = .{};
            self.has_despawn_hook = .{};
            self.add_hooks = .empty;
            self.remove_hooks = .empty;
            self.despawn_hooks = .empty;
        }

        pub fn runAddedHook(self: *Self, flag: FlagSet.Flag, comp: *anyopaque, entity: Entity, world: *World) !void {
            const hooks = self.add_hooks.get(flag) orelse return;
            for (hooks.items) |hook| try hook.run(comp, entity, world);
        }

        pub fn runRemoveHook(self: *Self, flag: FlagSet.Flag, comp: *anyopaque, entity: Entity, world: *World) !void {
            const hooks = self.remove_hooks.get(flag) orelse return;
            for (hooks.items) |hook| try hook.run(comp, entity, world);
        }

        pub fn runDespawnHook(self: *Self, flag: FlagSet.Flag, comp: *anyopaque, entity: Entity, world: *World) !void {
            const hooks = self.despawn_hooks.get(flag) orelse return;
            for (hooks.items) |hook| try hook.run(comp, entity, world);
        }

        pub fn OnRemoveComp(
            self: *Self,
            world: *App(desc),
            gpa: std.mem.Allocator,
            comptime T: type,
            comptime hook_fn: *const fn (*T, Entity, *World) EcsError!void,
        ) EcsError!void {
            const hook = Hook{ .run = (struct {
                fn run(ptr: *anyopaque, entity: Entity, w: *World) EcsError!void {
                    const comp: *T = @ptrCast(@alignCast(ptr));
                    try hook_fn(comp, entity, w);
                }
            }).run };

            const flag = world.components.component_flags.getFlag(T);
            self.has_remove_hook.insert(flag);

            const res = try self.remove_hooks.getOrPut(gpa, flag);
            if (!res.found_existing) res.value_ptr.* = .empty;
            try res.value_ptr.append(gpa, hook);
        }

        pub fn OnDespawnComp(
            self: *Self,
            world: *App(desc),
            gpa: std.mem.Allocator,
            comptime T: type,
            comptime hook_fn: *const fn (*T, Entity, *World) EcsError!void,
        ) EcsError!void {
            const hook = Hook{ .run = (struct {
                fn run(ptr: *anyopaque, entity: Entity, w: *World) EcsError!void {
                    const comp: *T = @ptrCast(@alignCast(ptr));
                    try hook_fn(comp, entity, w);
                }
            }).run };

            const flag = world.components.component_flags.getFlag(T);
            self.has_despawn_hook.insert(flag);

            const res = try self.despawn_hooks.getOrPut(gpa, flag);
            if (!res.found_existing) res.value_ptr.* = .empty;
            try res.value_ptr.append(gpa, hook);
        }

        pub fn OnAddComp(
            self: *Self,
            world: *App(desc),
            gpa: std.mem.Allocator,
            comptime T: type,
            comptime hook_fn: *const fn (*T, Entity, *World) EcsError!void,
        ) EcsError!void {
            const hook = Hook{ .run = (struct {
                fn run(ptr: *anyopaque, entity: Entity, w: *World) EcsError!void {
                    const comp: *T = @ptrCast(@alignCast(ptr));
                    try hook_fn(comp, entity, w);
                }
            }).run };

            const flag = world.components.component_flags.getFlag(T);
            self.has_add_hook.insert(flag);

            const res = try self.add_hooks.getOrPut(gpa, flag);
            if (!res.found_existing) res.value_ptr.* = .empty;
            try res.value_ptr.append(gpa, hook);
        }
    };
}

pub fn WorldAccess(desc: AppDesc) type {
    return struct {
        /// Full world access
        inner: *App(desc),

        pub fn addAccess(_: *App(desc), access: *Access(desc.FlagInt)) void {
            access.res_read_write = .full;
            access.res_write = .full;
            access.comp_read_write = .full;
            access.comp_write = .full;
        }

        pub fn fromWorld(app: *App(desc)) EcsError!@This() {
            return .{
                .inner = app,
            };
        }
    };
}
